CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      subroutine wvflxtn(CPSI,WKGD,XOUT,YOUT)
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C   sub for computing Takaya and Nakamura (JAS 2001) nondivergent 2-d stwvflx
C       CPSI: input north-to-south clim stream function
C       APSI: input north-to-south anom stream function
C       XFS & YFS: x & y components of wvflx
C   more subroutings needed are:
C           fftgcm.f  evlsca15.f  genpnme15.f   glats.f  norsou.f
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      PARAMETER(NW=15)
      PARAMETER(NLEG=NW+1)
      PARAMETER(NM=NW)
      PARAMETER(NMP=NW+1)
      PARAMETER(NLONG=64)
      PARAMETER(NLATG=40)
      PARAMETER(NLEGP=NLEG+1)
      PARAMETER(NLONGP=NLONG+1,IMAXT=3*NLONG/2)
      PARAMETER(KHALF=(NLATG+1)/2)
      COMMON/GAUSS/RADG(NLATG),GWGT(NLATG)
      COMMON/POLY1/PLEG(NLEG,NMP,NLATG),DPLEG(NLEG,NMP,NLATG)
      COMMON/FFTAR1/TRIGS(IMAXT),IFAX(10)
      COMMON/FFTAR2/ARRAY(NLONGP,NLATG),WORK(NLONGP,NLATG)
      COMMON/EPSLON/EPS(NLEG,NMP),XXN(NLEG,NMP),XXM(NMP)
      COMMON/CORCOS/CORIOLIS(NLATG)  
      REAL*8    COLRAD(KHALF),WGT(KHALF),WGTCS(KHALF),RCS2(KHALF)
      COMMON/STWV/AU(NLONG,NLATG),AV(NLONG,NLATG),
     1            APSI(NLONG,NLATG),
     2            XFS(NLONG,NLATG),YFS(NLONG,NLATG),
     6            CU(NLONG,NLATG),CV(NLONG,NLATG)
      DIMENSION CPSI(NLONG,NLATG)     
      DIMENSION QZER(2,NLEG,NMP)      
      DIMENSION STMFSP(2,NLEG,NMP)                     
      DIMENSION COSLAT(NLATG)
      DIMENSION XOUT(NLONG,NLATG),YOUT(NLONG,NLATG)     
      DIMENSION WKGD(NLONG,NLATG)    
      PI=ACOS(-1.0)
      LENS=2*NLEG*NMP
      LENG=NLONG*NLATG
      R = 6.371E+06
      RR=R**2
      UNDEF=-9.99E+33
      CALL GLATS(KHALF,COLRAD,WGT,WGTCS,RCS2)
      DO 30 K=1,KHALF
      RADG(K)=PI/2.0 - COLRAD(K)
      RADG(NLATG-K+1)=-RADG(K)
      GWGT(K)=WGT(K)
      GWGT(NLATG-K+1)=+GWGT(K)
   30 CONTINUE
      DO 40 JG=1,NLATG
      CALL GENPNME15(RADG(JG),PLEG(1,1,JG),DPLEG(1,1,JG))
   40 CONTINUE
C.....CALCULATE the CORIOLIS and COSLAT
      OMEGA=2*PI/(24.*3600.)
      DO 35 K=1,NLATG
         CORIOLIS(K)=2*OMEGA*SIN(RADG(K))
         COSLAT(K)=COS(RADG(K))
      WRITE(6,*) 'COSLAT=',K,COSLAT(K)          
   35 CONTINUE
C-----------------------------------------------------------------
C   CALCULATE ROTATIONAL WINDS FOR CLIM(NO DIVERGENCE)
C
      do i=1,NLONG
      do j=1,NLATG
         APSI(I,J)=WKGD(i,j)
      enddo
      enddo
C
      do 60 i=1,NLONG
      do 60 j=1,NLATG
         WKGD(i,j)=CPSI(i,j)
60    CONTINUE
      CALL transform15(WKGD,STMFSP)
65    CONTINUE
      write(6,*) 'before EVLUV'
      CALL FILLAR(QZER,0.,LENS)
      CALL EVLU15(QZER,STMFSP,CU)
      CALL EVLV15(QZER,STMFSP,CV)
C   CALCULATE ROTATIONAL WINDS FOR ANOM(NO DIVERGENCE)
C
      do 61 i=1,NLONG
      do 61 j=1,NLATG
         WKGD(i,j)=APSI(I,J)
61    CONTINUE
      CALL transform15(WKGD,STMFSP)
      write(6,*) 'before EVLUV'
      CALL FILLAR(QZER,0.,LENS)
      CALL EVLU15(QZER,STMFSP,AU)
      CALL EVLV15(QZER,STMFSP,AV)
C---CALCULATE WVFLX                              
      PRINT 103
  103 FORMAT(' befor STWVFLX_TN ')
      CALL STWVFLX_TN           
c     CALL norsou(XFS,XOUT,NLONG,NLATG)
c     CALL norsou(YFS,YOUT,NLONG,NLATG)
      do 70 i=1,NLONG
      do 70 j=1,NLATG
         XOUT(i,j)=XFS(i,j)
         YOUT(i,j)=YFS(i,j)
70    CONTINUE
      RETURN
      END
 
      SUBROUTINE EVLU15(VELP,STRM,ARRP)
      PARAMETER(NW=15)
      PARAMETER(NLEG=NW+1,NMP=NW+1)
      PARAMETER(NLATG=40,NLONG=64,NLONGP=NLONG+1,IMAXT=3*NLONG/2)
      PARAMETER(EA=6.371E+06)
      COMMON/GAUSS/RADG(NLATG),GWGT(NLATG)
      COMMON/POLY1/PLEG(NLEG,NMP,NLATG),DPLEG(NLEG,NMP,NLATG)
      COMMON/FFTAR1/TRIGS(IMAXT),IFAX(10)
      COMMON/FFTAR2/ARRAY(NLONGP,NLATG),WORK(NLONGP,NLATG)
      DIMENSION VELP(2,NLEG,NMP),STRM(2,NLEG,NMP)
      DIMENSION ARRP(NLONG,NLATG)
      IM1=2*NMP+1
      IM2=NLONGP
      DO 200 J=1,NLATG
      CONST=EA*COS(RADG(J))
      DO 120 IM=1,NMP
      XM=IM-1
      YSUMR=0.0
      YSUMI=0.0
      IF(IM .GT. 1) THEN
         NLOW=1
      ELSE
         NLOW=2
      END IF
      DO 100 N=NLOW,NLEG
      XN=XM+N-1
      YSUMR=YSUMR+(-XM*VELP(2,N,IM)* PLEG(N,IM,J)
     1             -   STRM(1,N,IM)*DPLEG(N,IM,J))
      YSUMI=YSUMI+( XM*VELP(1,N,IM)* PLEG(N,IM,J)
     1             -   STRM(2,N,IM)*DPLEG(N,IM,J))
  100 CONTINUE
      ARRAY((IM-1)*2+1,J)=YSUMR/CONST
      ARRAY((IM-1)*2+2,J)=YSUMI/CONST
  120 CONTINUE
      DO 130 IM=IM1,IM2
      ARRAY(IM,J)=0.0
  130 CONTINUE
  200 CONTINUE
      INC=1
      ISIGN=+1
      JUMP=NLONGP
      LOT=NLATG
      CALL FFT991(ARRAY,WORK,TRIGS,IFAX,INC,JUMP,NLONG,LOT,ISIGN)
      DO 240 J=1,NLATG
      DO 240 I=1,NLONG
      ARRP(I,J)=ARRAY(I,J)
  240 CONTINUE
CC    CALL WNDWR(FIELDG)
      RETURN
      END



      SUBROUTINE EVLV15(VELP,STRM,ARRP)
      PARAMETER(NW=15)
      PARAMETER(NLEG=NW+1,NMP=NW+1)
      PARAMETER(NLATG=40,NLONG=64,NLONGP=NLONG+1,IMAXT=3*NLONG/2)
      PARAMETER(EA=6.371E+06)
      COMMON/GAUSS/RADG(NLATG),GWGT(NLATG)
      COMMON/POLY1/PLEG(NLEG,NMP,NLATG),DPLEG(NLEG,NMP,NLATG)
      COMMON/FFTAR1/TRIGS(IMAXT),IFAX(10)
      COMMON/FFTAR2/ARRAY(NLONGP,NLATG),WORK(NLONGP,NLATG)
      DIMENSION VELP(2,NLEG,NMP),STRM(2,NLEG,NMP)
      DIMENSION ARRP(NLONG,NLATG)
      IM1=2*NMP+1
      IM2=NLONGP
      DO 200 J=1,NLATG
      CONST=EA*COS(RADG(J))
      DO 120 IM=1,NMP
      XM=IM-1
      YSUMR=0.0
      YSUMI=0.0
      IF(IM .GT. 1) THEN
         NLOW=1
      ELSE
         NLOW=2
      END IF
      DO 100 N=NLOW,NLEG
      XN=XM+N-1
      YSUMR=YSUMR+(-XM*STRM(2,N,IM)* PLEG(N,IM,J)
     1             +   VELP(1,N,IM)*DPLEG(N,IM,J))
      YSUMI=YSUMI+( XM*STRM(1,N,IM)* PLEG(N,IM,J)
     1             +   VELP(2,N,IM)*DPLEG(N,IM,J))
  100 CONTINUE
      ARRAY((IM-1)*2+1,J)=YSUMR/CONST
      ARRAY((IM-1)*2+2,J)=YSUMI/CONST
  120 CONTINUE
      DO 130 IM=IM1,IM2
      ARRAY(IM,J)=0.0
  130 CONTINUE
  200 CONTINUE
      INC=1
      ISIGN=+1
      JUMP=NLONGP
      LOT=NLATG
      CALL FFT991(ARRAY,WORK,TRIGS,IFAX,INC,JUMP,NLONG,LOT,ISIGN)
      DO 240 J=1,NLATG
      DO 240 I=1,NLONG
      ARRP(I,J)=ARRAY(I,J)
  240 CONTINUE
      RETURN
      END



      SUBROUTINE DEL2(ARRAYI,ARRAYO)
      PARAMETER(NW=15)
      PARAMETER(NLEG=NW+1,NM=NW,NMP=NM+1)
      PARAMETER(EA=6.371E+06)
      DIMENSION ARRAYI(2,NLEG,NMP),ARRAYO(2,NLEG,NMP)
      DO 100 IM=1,NMP
      XM=IM-1
      DO 80 N=1,NLEG
      XN=N+XM-1.0
      IF(XN .LT. .001) THEN
         ARRAYO(1,N,IM)=0.0
         ARRAYO(2,N,IM)=0.0
      ELSE
         DELSQI=-(XN*(XN+1.0))/(EA*EA)
         ARRAYO(1,N,IM)=ARRAYI(1,N,IM)*DELSQI
         ARRAYO(2,N,IM)=ARRAYI(2,N,IM)*DELSQI
      END IF
   80 CONTINUE
  100 CONTINUE
      RETURN
      END


      SUBROUTINE transform15(FIELDG,FLDSPC)
      PARAMETER(NW=15)
      PARAMETER(NLEG=NW+1)
      PARAMETER(NM=NW)
      PARAMETER(NMP=NW+1)
      PARAMETER(NLONG=64)
      PARAMETER(NLATG=40)
      PARAMETER(NLONGP=NLONG+1,IMAXT=3*NLONG/2)
      PARAMETER(KHALF=(NLATG+1)/2)
      COMMON/GAUSS/RADG(NLATG),GWGT(NLATG)
      COMMON/POLY1/PLEG(NLEG,NMP,NLATG),DPLEG(NLEG,NMP,NLATG)
      COMMON/FFTAR1/TRIGS(IMAXT),IFAX(10)
      COMMON/FFTAR2/ARRAY(NLONGP,NLATG),WORK(NLONGP,NLATG)
      DIMENSION FIELDG(NLONG,NLATG)
      DIMENSION FLDSPC(2,NLEG,NMP)
      DO 20 IM=1,NMP
      DO 20 IN=1,NLEG
      DO 20 IC=1,2
      FLDSPC(IC,IN,IM)=0.0
   20 CONTINUE
      DO 60 JG=1,NLATG
      DO 40 IG=1,NLONG
      ARRAY(IG,JG)=FIELDG(IG,JG)
   40 CONTINUE
      ARRAY(NLONGP,JG)=0.0
   60 CONTINUE
      ISIGN=-1
      INC=1
      JUMP=NLONGP
      LOT=NLATG
      CALL FFT991(ARRAY,WORK,TRIGS,IFAX,INC,JUMP,NLONG,LOT,ISIGN)
      DO 200 JG=1,NLATG
      DO 180 IM=1,NMP
      DO 180 IN=1,NLEG
      FLDSPC(1,IN,IM)=FLDSPC(1,IN,IM)+ARRAY(2*IM-1,JG)*PLEG(IN,IM,JG)*
     1 GWGT(JG)
      FLDSPC(2,IN,IM)=FLDSPC(2,IN,IM)+ARRAY(2*IM  ,JG)*PLEG(IN,IM,JG)*
     1 GWGT(JG)
  180 CONTINUE
  200 CONTINUE
      RETURN
      END


 
      SUBROUTINE FILLAR(ARRAYS,VALUE,N)
      DIMENSION ARRAYS(N)
      DO 20 I=1,N
      ARRAYS(I)=VALUE
   20 CONTINUE
      RETURN
      END


      SUBROUTINE STWVFLX_TN           
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C    PROGRAM TO compute the Plumb stationary wave activity flux using  C
C    PRESSURE HISTORY FIELDS.                                          C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      PARAMETER(NW=15)
      PARAMETER(NLEG=NW+1)
      PARAMETER(NM=NW)
      PARAMETER(NMP=NW+1)
      PARAMETER(NLONG=64)
      PARAMETER(NLATG=40)
      COMMON/GAUSS/RADG(NLATG),GWGT(NLATG)
      COMMON/EPSLON/EPS(NLEG,NMP),XXN(NLEG,NMP),XXM(NMP)
      COMMON/CORCOS/CORIOLIS(NLATG)  
      COMMON/STWV/AU(NLONG,NLATG),AV(NLONG,NLATG),
     1            APSI(NLONG,NLATG),
     2            XFS(NLONG,NLATG),YFS(NLONG,NLATG),
     3            CU(NLONG,NLATG),CV(NLONG,NLATG)
      DIMENSION USP(2,NLEG,NMP),VSP(2,NLEG,NMP)
      DIMENSION DUDXSP(2,NLEG,NMP),DVDXSP(2,NLEG,NMP)
      DIMENSION DUDYSP(2,NLEG,NMP),DVDYSP(2,NLEG,NMP)
      DIMENSION DUDX(NLONG,NLATG),DUDY(NLONG,NLATG)
      DIMENSION DVDX(NLONG,NLATG),DVDY(NLONG,NLATG)
      R=6.371E+06
C.....compute du/dx, du/dy and dv/dx in spectral
         CALL transform15(AU,USP)
         CALL transform15(AV,VSP)
      CALL ZNGRSP(USP,DUDXSP)
      CALL ZNGRSP(VSP,DVDXSP)
      CALL MDGRSP(USP,DUDYSP)
CC....transform spectral gradients to gaussian grids
         CALL EVLSCA(DUDXSP,DUDX)
         CALL EVLSCA(DVDXSP,DVDX)
         CALL EVLSCA(DUDYSP,DUDY)
      DO 270 J=1,NLATG
      DO 270 I=1,NLONG
         DUDX(I,J)=DUDX(I,J)/COS(RADG(J))   
         DVDX(I,J)=DVDX(I,J)/COS(RADG(J))   
  270 CONTINUE
C.....compute  components of wvflx                       
      DO 280 J=1,NLATG
      DO 280 I=1,NLONG
      F1=COS(RADG(J))
      F2=2*sqrt(CU(I,J)**2+CV(I,J)**2)               
      XFS(I,J)=F1*(CU(I,J)*(AV(I,J)**2-APSI(I,J)*DVDX(I,J))+
     &         CV(I,J)*(APSI(I,J)*DUDX(I,J)-AU(I,J)*AV(I,J)))/F2
      YFS(I,J)=F1*(CU(I,J)*(APSI(I,J)*DUDX(I,J)-AU(I,J)*AV(I,J))+
     &         CV(I,J)*(AU(I,J)**2+APSI(I,J)*DUDY(I,J)))/F2
  280 CONTINUE

      RETURN
      END
 

CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C   zonal mean 2-D field                 
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      SUBROUTINE ZMEAN(FLDIN,FLDOT)
      PARAMETER(NLONG=64,NLATG=40)
      DIMENSION FLDIN(NLONG,NLATG),FLDOT(NLATG)
      XX=FLOAT(NLONG)
      DO 10 J=1,NLATG
         FLDOT(J)=0.
   10 CONTINUE
      DO 100 J=1,NLATG
      DO 100 I=1,NLONG
         FLDOT(J)=FLDOT(J)+FLDIN(I,J)/XX      
  100 CONTINUE
      RETURN
      END
      
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C  1-D zonal gradient in spectral space
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      SUBROUTINE ZNGRSP(FILDIN,OUTX)
      PARAMETER(NW=15,NLEG=NW+1,NMP=NW+1)
      COMMON/EPSLON/EPS(NLEG,NMP),XXN(NLEG,NMP),XXM(NMP)
      DIMENSION FILDIN(2,NLEG,NMP)                   
      DIMENSION OUTX(2,NLEG,NMP),OUTY(2,NLEG,NMP)
      R=6.371E+06
CC....x component of the gradient
      DO 100 K=1,NMP
      DO 100 J=1,NLEG 
         OUTX(1,J,K)=-XXM(K)*FILDIN(2,J,K)/R
         OUTX(2,J,K)=XXM(K)*FILDIN(1,J,K)/R
  100 CONTINUE
      RETURN
      END
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C  1-D meridional gradient in spectral space
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      SUBROUTINE MDGRSP(FILDIN,OUTY)
      PARAMETER(NW=15,NLEG=NW+1,NMP=NW+1)
      COMMON/EPSLON/EPS(NLEG,NMP),XXN(NLEG,NMP),XXM(NMP)
      DIMENSION FILDIN(2,NLEG,NMP)                   
      DIMENSION OUTY(2,NLEG,NMP)
      R=6.371E+06
CC....y component of the gradient
      DO 200 I=1,2
      DO 200 K=1,NMP
         DO 300 J=2,NW
      OUTY(I,J,K)=-((XXN(J,K)-1)*EPS(J,K)*FILDIN(I,J-1,K)-
     1(XXN(J,K)+2)*EPS(J+1,K)*FILDIN(I,J+1,K))/R
  300 CONTINUE
      OUTY(I,1,K)=+(XXN(1,K)+2)*EPS(2,K)*FILDIN(I,2,K)
     1/R
      OUTY(I,NLEG,K)=-(XXN(NLEG,K)-1)*EPS(NLEG,K)*
     1FILDIN(I,NW,K)/R
  200 CONTINUE
      RETURN
      END

