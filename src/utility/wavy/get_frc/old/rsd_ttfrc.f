      PROGRAM NLCONT
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      PARAMETER ( NW=15, NLV=10 )
C     PARAMETER ( NXOUT=7,NYOUT=16)
      PARAMETER ( NXOUT=16,NYOUT=16)
c     PARAMETER ( NXOUT=11,NYOUT=16)
      PARAMETER( NLOGG=64, NLAGG=40 )
*DECK NLCONT
C.......FULL TENDENCIES DUE TO TIME MEAN FLOW
C.......USES SIGMADOT CALCULATED FROM TIME MEAN FLOW
*CALL NLC
      PARAMETER(NFFTI=2*NLOGG+15)
      PARAMETER ( NWP1=NW+1, NWP2=NW+2, NLVP1=NLV+1,
     & NCT1=NWP1*NWP1 , NCT2=NWP1*NWP2 ,NDM=5*NWP1 )
      PARAMETER(NHARM=2*NWP1*NWP1)
      DIMENSION  BUFR( NHARM , NLV)
      DIMENSION  BUFR2(NHARM)
      DIMENSION  BUFR3(2*NXOUT*NYOUT)
      COMPLEX FF(NDM,NLV),H0(NWP1,NWP1)
      LOGICAL LTRAN
      real*8 DCOLRAD(NLAGG), DWGT(NLAGG),
     1       DWGTCS(NLAGG), DRCS2(NLAGG)
      COMMON /GRDSPC/
     *  PLN(NCT2), QLN(NCT2),EPS(NWP1,NWP2),
     *  COLRAD(NLAGG), WGT(NLAGG), WGTCS(NLAGG), RCS2(NLAGG),
     *  SI(NLVP1), SL(NLV), DEL(NLV), RPI(NLV)
      COMPLEX Q,ULN,VLN,TM,DI,ZE,NLFORV,NLFORD,NLFORT,NLFORC,
     *  DPDLAM,DPDPHI,GEO,DOT
      COMMON /SPCIN/
     *      Q(NWP1,NWP1),     ULN(NCT2,NLV),    VLN(NCT2,NLV),
     *      TM(NCT1,NLV),    DI(NCT1,NLV),    ZE(NCT1,NLV),
     *      GEO(NCT1,NLV),   DOT(NCT1,NLV),   TSFC(NWP1,NWP1)
      COMMON /SPCOUT/
     *     NLFORV(NWP1,NWP1,NLV), NLFORD(NWP1,NWP1,NLV),
     *     NLFORT(NWP1,NWP1,NLV), NLFORC(NWP1,NWP1,NLV)
      COMMON /SPCTMP/
     *     DPDLAM(NCT1),      DPDPHI(NWP1,NWP2)
      COMMON / TGLB / TGLOB(10)
      COMMON /NCARFT/ SCNMC(NFFTI)
C
      DIMENSION CI(NLVP1),CL(NLV)
      DIMENSION SSSGGG(NLOGG,NLAGG,NLV),OUT(NLOGG,NLAGG)
C
      COMPLEX TOTFRC(NWP1,NWP1,NLV,5), DBH(NCT1,NLV)            
C.......................................................................
      open(unit=10,form='unformatted',access='direct',recl=8*NWP1*NWP1)
      open(unit=46,form='unformatted',access='direct',
     &recl=8*NWP1*NWP1*NLV*5)
C
      WRITE(6,*) ' NLCONT FOR NW ',NW,' NYOUT ',NYOUT
C....GET GAUSSIAN LATITUDES AND WEIGHTS
      CALL AGLATF (NLAGG,DCOLRAD,DWGT,DWGTCS,DRCS2)
      DO 200 ii=1,NLAGG
         COLRAD(ii)=real(DCOLRAD(ii))
         WGT(ii)=real(DWGT(ii))
         WGTCS(ii)=real(DWGTCS(ii))
         RCS2(ii)=real(DRCS2(ii))
200   CONTINUE
C
C....GET VERTICAL GRID
      CALL ASTSIG (CI,SI,DEL,SL,CL,RPI,NLV)
      write(6,*) 'after ASTSIG'
C
C....GET EPSILON
      CALL AEPS(EPS,NW)
C
C....READ SPECTRAL FIELDS
      INFILE = 10
      LTRAN=.TRUE.
      IREAD=0
      CALL READ85 (INFILE,BUFR2,IREAD,1,Q,NWP1,LTRAN)
      IREAD=IREAD+1
      CALL READ85 (INFILE,BUFR2,IREAD,NLV,DI,NWP1,LTRAN)
      IREAD=IREAD+NLV
      CALL READ85 (INFILE,BUFR2,IREAD,NLV,ZE,NWP1,LTRAN)
      IREAD=IREAD+NLV
      CALL READ85 (INFILE,BUFR2,IREAD,NLV,TM,NWP1,LTRAN)
      IREAD=IREAD+NLV
      CALL READ85 (INFILE,BUFR2,IREAD,1,TSFC,NWP1,LTRAN)
      IREAD=IREAD+1
      CALL READ85 (INFILE,BUFR2,IREAD,1,H0,NWP1,LTRAN)
      write(6,*) 'have read data'
C
C....CALCULATE GEOPOTENTIAL
      CALL GEOMEA(TM,H0,GEO)
      write(6,*) 'GEO=',GEO(1,6),'H0=',H0(1,5)
C....GLOBAL MEAN TEMPERATURES AS A FUNCTION OF HEIGHT
      DO 1 K=1,NLV
      TGLOB(K) = REAL(TM(1,K))/SQRT(2.)
      write(6,*) 'TGLOB=',TGLOB(K)
   1  CONTINUE
C
C....GET U, V, DPDLAM, DPDPHI SPECTRAL
      DO 22 K=1, NLV
      CALL ADZTUV (DI(1,K),ZE(1,K),ULN(1,K),VLN(1,K),EPS,NW)
      write(6,*) 'U&V=',ULN(1,K),VLN(1,K)
  22  CONTINUE
      CALL DELLNP (Q, DPDPHI, DPDLAM, EPS, NW)
C....RELATIVE VORTICITY TO ABSOLUTE VORTICITY
      CALL ABSVOR (ZE,NWP1,NLV)
      write(6,*) 'ABS VOR=',ZE(1,3)
C....GO TO GRID
      CALL ACOFIL(NLFORV,2*NCT1*NLV,0.0)
      CALL ACOFIL(NLFORD,2*NCT1*NLV,0.0)
      CALL ACOFIL(NLFORT,2*NCT1*NLV,0.0)
      CALL ACOFIL(NLFORC,2*NCT1*NLV,0.0)
      write(6,*) 'before RFFTI'
      CALL RFFTI (NLOGG,SCNMC)
      write(6,*) 'before LTLOOP'
      CALL LTLOOP(SSSGGG)
C
C   store the totfrc 
      CALL FILLCM(TOTFRC(1,1,1,1),NCT1*NLV,NLFORV)
      CALL FILLCM(TOTFRC(1,1,1,2),NCT1*NLV,NLFORD)
      CALL FILLCM(TOTFRC(1,1,1,3),NCT1*NLV,NLFORT)
      CALL FILLCM(TOTFRC(1,1,1,4),NCT1*NLV,NLFORC)
      CALL FILLCM(TOTFRC(1,1,1,5),NCT1*NLV,DBH)
C
      WRITE(46,rec=1) TOTFRC
      WRITE(6,*) NLFORV(1,1,6)
C
      STOP
      END
C
*DECK LTLOOP
      SUBROUTINE LTLOOP(SSSGGG)
*CALL NLC
      PARAMETER ( NW=15, NLV=10 )
      PARAMETER( NLOGG=64, NLAGG=40 )
      PARAMETER ( NWP1=NW+1, NWP2=NW+2, NLVP1=NLV+1,
     & NCT1=NWP1*NWP1 , NCT2=NWP1*NWP2 )
      COMMON /GRDSPC/
     *  PLN(NCT2), QLN(NCT2),EPS(NWP1,NWP2),
     *  COLRAD(NLAGG), WGT(NLAGG), WGTCS(NLAGG), RCS2(NLAGG),
     *  SI(NLVP1), SL(NLV), DEL(NLV), RPI(NLV)
      COMPLEX Q,ULN,VLN,TM,DI,ZE,NLFORV,NLFORD,NLFORT,NLFORC,
     *  DPDLAM,DPDPHI,GEO,DOT
      COMPLEX NLDUM(NCT1,NLV)
      COMMON /SPCIN/
     *     Q(NCT1),          ULN(NCT2,NLV),    VLN(NCT2,NLV),
     *      TM(NCT1,NLV),    DI(NCT1,NLV),    ZE(NCT1,NLV),
     *     GEO(NCT1,NLV),    DOT(NCT1,NLV),   TSFC(NCT1)
      COMMON /SPCOUT/
     *     NLFORV(NCT1,NLV), NLFORD(NCT1,NLV),
     *     NLFORT(NCT1,NLV),NLFORC(NCT1,NLV)
      COMMON /SPCTMP/
     *     DPDLAM(NCT1),      DPDPHI(NCT2)
      COMMON /GRDIN/
     *             QG(NLOGG),TSG(NLOGG),DLAM(NLOGG),DPHI(NLOGG),
     *             DG(NLOGG,NLV), UG(NLOGG,NLV),   VG(NLOGG,NLV),
     *             SG(NLOGG,NLV), TG(NLOGG,NLV),   ZEG(NLOGG,NLV)
      COMMON /GRDOUT/
     *          DQDTM(NLOGG,NLV),
     *          DTDTA(NLOGG,NLV), DTDTB(NLOGG,NLV), DTDTC(NLOGG,NLV),
     *          DVDTA(NLOGG,NLV), DVDTB(NLOGG,NLV),
     *          DDDTA(NLOGG,NLV), DDDTB(NLOGG,NLV), DDDTC(NLOGG,NLV),
     *          VDVOR(NLOGG,NLV), VDDIV(NLOGG,NLV), VDTEM(NLOGG,NLV)
      DIMENSION SS1(2,NWP1), SS2(2,NWP1), SS3(2,NWP1,NWP2)
      DIMENSION SSGG(NLOGG,NLV),SSSGGG(NLOGG,NLAGG,NLV)
C
      RAD = 6.37E6
      RGAS = 287.05
      PI = 4.*ATAN(1.0)
C
      ccolat = 0.
      WRITE(6,*) ' FORCING IN NLCONT ZERO FOR COLAT LT ',CCOLAT
      CALL ACOFIL(NLDUM,2*NCT1*NLV,0.0)
      DO 1000 LAT = 1, NLAGG
      IF(COLRAD(LAT).LE.CCOLAT) GO TO 1000
      WRITE(6,*) ' LAT=',LAT
C
      CALL APLN2 (PLN,COLRAD,NLAGG,LAT,EPS,NW,SS1,SS2,SS3)
      CALL AQLN  (PLN,QLN,EPS,NWP1)
C
C........EVALUATE AT MU
      CALL STOYGR(Q,QG,NWP1,NWP1,PLN)
      CALL STOYGR(TSFC,TSG,NWP1,NWP1,PLN)
      CALL STOYGR(DPDLAM,DLAM,NWP1,NWP1,PLN)
      CALL STOYGR(DPDPHI,DPHI,NWP1,NWP2,PLN)
C
      DO 100 K=1,NLV
      CALL STOYGR(DI(1,K),DG(1,K),NWP1,NWP1,PLN)
      CALL STOYGR(ULN(1,K),UG(1,K),NWP1,NWP2,PLN)
      CALL STOYGR(VLN(1,K),VG(1,K),NWP1,NWP2,PLN)
      CALL STOYGR(TM(1,K), TG(1,K),NWP1,NWP1,PLN)
      CALL STOYGR(ZE(1,K), ZEG(1,K),NWP1,NWP1,PLN)
      CALL STOYGR(DOT(1,K), SG(1,K),NWP1,NWP1,PLN)
  100 CONTINUE
C
      RCL=RCS2(LAT)
      CALL LGLOOP(DEL,SI,SL,RPI,RCL,SSGG)
          do 101 k=1,NLV
          do 101 i=1,NLOGG
             SSSGGG(i,LAT,k)=SSGG(i,k)
101       continue
      DO 200 K=1,NLV
      CALL GGRTOS(DQDTM(1,K),NLFORC(1,K),WGT(LAT),PLN,NWP1)
      CALL GGRTOS(DTDTA(1,K),NLFORT(1,K),WGT(LAT),PLN,NWP1)
      CALL GGRTOS(DTDTB(1,K),NLFORT(1,K),WGT(LAT),QLN,NWP1)
      CALL GGRTOS(DTDTC(1,K),NLFORT(1,K),WGT(LAT),PLN,NWP1)
      CALL GGRTOS(VDTEM(1,K),NLFORT(1,K),WGT(LAT),PLN,NWP1)
      CALL GGRTOS(DVDTA(1,K),NLFORV(1,K),WGT(LAT),PLN,NWP1)
      CALL GGRTOS(DVDTB(1,K),NLFORV(1,K),WGT(LAT),QLN,NWP1)
      CALL GGRTOS(VDVOR(1,K),NLFORV(1,K),WGT(LAT),PLN,NWP1)
      CALL GGRTOS(DDDTA(1,K),NLFORD(1,K),WGT(LAT),PLN,NWP1)
      CALL GGRTOS(DDDTB(1,K),NLFORD(1,K),WGT(LAT),QLN,NWP1)
      CALL GGRTOS(DDDTC(1,K),NLDUM (1,K),WGT(LAT),PLN,NWP1)
      CALL GGRTOS(VDDIV(1,K),NLFORD(1,K),WGT(LAT),PLN,NWP1)
 200  CONTINUE
C
 1000 CONTINUE
 1001 CONTINUE
C
      DO 400 K=1,NLV
      DO 400 I=1,NCT1
      IF(K.LT.NLV) THEN
      NLDUM(I,K) = NLDUM(I,K) - GEO(I,K+1)*SI(K+1)/(DEL(K)*RAD**2)
      END IF
      NLDUM(I,K) = NLDUM(I,K) + GEO(I,K)*SI(K)/(DEL(K)*RAD**2)
      NLDUM(I,K) = NLDUM(I,K) + RGAS*TM(I,K)/RAD**2
 400  CONTINUE
      CALL DELSQR(NLDUM,NLDUM,NWP1,NLV)
      DO 300 I=1,NCT1
      DO 300 K=1,NLV
      NLFORD(I,K)=NLFORD(I,K) + NLDUM(I,K)
 300  CONTINUE
C.......DEL FOURTH DAMPING TERMS
      C0V = 1.E17
      C0T = 1.E17
      CDELV = C0V
      CDELD = C0T
      CDELT = C0T
C
      CALL DELSQR(ZE,NLDUM,NWP1,NLV)
      CALL DELSQR(NLDUM,NLDUM,NWP1,NLV)
      DO 301 I=1,NCT1
      DO 301 K=1,NLV
      NLFORV(I,K)=NLFORV(I,K) +CDELV*NLDUM(I,K)/RAD**4
 301  CONTINUE
C
      CALL DELSQR(DI,NLDUM,NWP1,NLV)
      CALL DELSQR(NLDUM,NLDUM,NWP1,NLV)
      DO 302 I=1,NCT1
      DO 302 K=1,NLV
      NLFORD(I,K)=NLFORD(I,K) +CDELD*NLDUM(I,K)/RAD**4
 302  CONTINUE
C
      CALL DELSQR(TM,NLDUM,NWP1,NLV)
      CALL DELSQR(NLDUM,NLDUM,NWP1,NLV)
      DO 303 I=1,NCT1
      DO 303 K=1,NLV
      NLFORT(I,K)=NLFORT(I,K) +CDELT*NLDUM(I,K)/RAD**4
 303  CONTINUE
C
      RETURN
      END
*DECK LGLOOP
      SUBROUTINE LGLOOP(DEL,SI,SL,RPI,RCL,SSGG)
*CALL NLC
      PARAMETER ( NW=15, NLV=10 )
      PARAMETER( NLOGG=64, NLAGG=40 )
      PARAMETER(NFFTI=2*NLOGG+15)
      COMMON /GRDIN/
     *             QG(NLOGG),TSG(NLOGG),DLAM(NLOGG),DPHI(NLOGG),
     *             DG(NLOGG,NLV), UG(NLOGG,NLV),   VG(NLOGG,NLV),
     *             SG(NLOGG,NLV), TG(NLOGG,NLV),   ZEG(NLOGG,NLV)
      COMMON /GRDOUT/
     *          DQDTM(NLOGG,NLV),
     *          DTDTA(NLOGG,NLV), DTDTB(NLOGG,NLV), DTDTC(NLOGG,NLV),
     *          DVDTA(NLOGG,NLV), DVDTB(NLOGG,NLV),
     *          DDDTA(NLOGG,NLV), DDDTB(NLOGG,NLV), DDDTC(NLOGG,NLV),
     *          VDVOR(NLOGG,NLV), VDDIV(NLOGG,NLV), VDTEM(NLOGG,NLV)
      DIMENSION  DEL( NLV ) , RPI( NLV ) , SI( NLV+1 ) , SL( NLV )
      DIMENSION  SSGG(NLOGG,NLV) 
      COMMON /NCARFT/ SCNMC(NFFTI)
      CALL AFFSFS (DG,SCNMC, NLV ,NLOGG,NFFTI,NW)
      CALL AFFSFS (UG,SCNMC, NLV ,NLOGG,NFFTI,NW)
      CALL AFFSFS (VG,SCNMC, NLV ,NLOGG,NFFTI,NW)
      CALL AFFSFS (TG,SCNMC, NLV ,NLOGG,NFFTI,NW)
      CALL AFFSFS (ZEG,SCNMC, NLV ,NLOGG,NFFTI,NW)
      CALL AFFSFS (SG,SCNMC, NLV ,NLOGG,NFFTI,NW)
      CALL AFFSFS (DLAM,SCNMC, 1 ,NLOGG,NFFTI,NW)
      CALL AFFSFS (DPHI,SCNMC, 1 ,NLOGG,NFFTI,NW)
      CALL AFFSFS (QG,  SCNMC, 1 ,NLOGG,NFFTI,NW)
      CALL AFFSFS (TSG, SCNMC, 1 ,NLOGG,NFFTI,NW)
C
      CALL CTEND (DG,UG,VG,DLAM,DPHI,SG,DQDTM,DEL,RCL,NLOGG,NLV)
           do 100 k=1,NLV
           do 100 i=1,NLOGG
              SSGG(i,k)=SG(i,k)
100        continue
      CALL TTEND (DG,UG,VG,TG,SG,DLAM,DPHI,
     *            DTDTA,DTDTB,DTDTC,DEL,RPI,RCL,NLOGG,NLV)
      CALL VTEND (ZEG,UG,VG,DLAM,DPHI,SG,TG,
     *            DVDTA,DVDTB,DEL,RCL,NLOGG,NLV)
      CALL DTEND (ZEG,UG,VG,DLAM,DPHI,SG,TG,
     *            DDDTA,DDDTB,DDDTC,DEL,RCL,NLOGG,NLV)
C
C-----Rayleigh damping & Newtonian colling
      CALL LNDAMP(ZEG,VDVOR,DG ,VDDIV,TG,VDTEM,
     *           DEL,SI,SL,NLOGG,NLV)
C-----vertical diffusion
      CALL DIFFUS (ZEG,TSG,VDVOR,DEL,SI,SL,NLOGG,NLV,0)
      CALL DIFFUS (DG ,TSG,VDDIV,DEL,SI,SL,NLOGG,NLV,0)
      CALL DIFFUS (TG ,TSG,VDTEM,DEL,SI,SL,NLOGG,NLV,1)
C
      CALL AFFAFA (DQDTM,DQDTM,SCNMC,NLV,NLOGG,NFFTI,NW)
      CALL AFFAFA (DTDTA,DTDTA,SCNMC,NLV,NLOGG,NFFTI,NW)
      CALL AFFAFA (DTDTB,DTDTB,SCNMC,NLV,NLOGG,NFFTI,NW)
      CALL AFFAFA (DTDTC,DTDTC,SCNMC,NLV,NLOGG,NFFTI,NW)
      CALL AFFAFA (VDTEM,VDTEM,SCNMC,NLV,NLOGG,NFFTI,NW)
      CALL AFFAFA (DVDTA,DVDTA,SCNMC,NLV,NLOGG,NFFTI,NW)
      CALL AFFAFA (DVDTB,DVDTB,SCNMC,NLV,NLOGG,NFFTI,NW)
      CALL AFFAFA (VDVOR,VDVOR,SCNMC,NLV,NLOGG,NFFTI,NW)
      CALL AFFAFA (DDDTA,DDDTA,SCNMC,NLV,NLOGG,NFFTI,NW)
      CALL AFFAFA (DDDTB,DDDTB,SCNMC,NLV,NLOGG,NFFTI,NW)
      CALL AFFAFA (DDDTC,DDDTC,SCNMC,NLV,NLOGG,NFFTI,NW)
      CALL AFFAFA (VDDIV,VDDIV,SCNMC,NLV,NLOGG,NFFTI,NW)
C
      DO 1 K=1,NLV
      CALL DDLAM (DTDTA(1,K),NW+1)
      CALL DDLAM (DVDTA(1,K),NW+1)
      CALL DDLAM (DDDTA(1,K),NW+1)
  1   CONTINUE
C
      RETURN
      END
*DECK CTEND
      SUBROUTINE CTEND (DG,UG,VG,DLAM,DPHI,SG,DQDTM,DEL,RCL,NLOGG,NLV)
      DIMENSION
     *  DG( NLOGG , NLV ),  UG( NLOGG , NLV ),  VG( NLOGG , NLV ),
     *  DLAM( NLOGG ),      DPHI( NLOGG ),      SG( NLOGG , NLV ),
     *  DQDTM( NLOGG, NLV )
      DIMENSION  DEL( NLV )
      DIMENSION  CB(64)
C
      RAD = 6.37E6
C
      DO 3 LE=1, NLV
      DO 2 LO=1, NLOGG
      DQDTM(LO,LE)=UG(LO,LE)*DLAM(LO)*RCL+VG(LO,LE)*DPHI(LO)*RCL
     *             +DG(LO,LE)
2     CONTINUE
3     CONTINUE
      DO 10 LO=1,NLOGG
      CB(LO) = DEL(1)*DQDTM(LO,1)
  10  CONTINUE
      DO 20 LE=1, NLV-1
      DO 20 LO=1, NLOGG
      CB(LO) = CB(LO) + DEL(LE+1)*DQDTM(LO,LE+1)
  20  CONTINUE
C.. SIGMA DOT COMPUTED ONLY AT INTERIOR INTERFACES.
      DO 30 LO=1, NLOGG
      SG(LO,1) = DEL(1)*(CB(LO)-DQDTM(LO,1))
      SG(LO,NLV) = 0.
 30   CONTINUE
      DO 40 K=1, NLV-2
      DO 40 LO=1, NLOGG
      SG(LO,K+1)= SG(LO,K) + DEL(K+1)*(CB(LO)-DQDTM(LO,K+1))
  40  CONTINUE
C
      DO 4 LE=1, NLV
      DO 4 LO=1, NLOGG
      IF(LE.EQ.1) THEN
        DOTM = 0.
         ELSE
        DOTM = SG( LO, LE-1 )
      END IF
      IF(LE.EQ.NLV) THEN
        DOTP = 0.
         ELSE
        DOTP = SG( LO, LE )
      END IF
      DQDTM(LO,LE) = DQDTM(LO,LE) + (DOTP-DOTM)/DEL(LE)
4     CONTINUE
      RETURN
      END
*DECK TTEND
      SUBROUTINE TTEND (DG,UG,VG,TG,SG,DLAM,DPHI,
     *            DTDTA,DTDTB,DTDTC,DEL,RPI,RCL,NLOGG,NLV)
      DIMENSION
     *  DG( NLOGG , NLV ),  UG( NLOGG , NLV ),  VG( NLOGG , NLV ),
     *  DLAM( NLOGG ),      DPHI( NLOGG ),      SG( NLOGG , NLV ),
     *  TG( NLOGG, NLV ),
     *  DTDTA( NLOGG, NLV ),DTDTB( NLOGG, NLV ),DTDTC( NLOGG, NLV )
      DIMENSION  DEL( NLV ) , RPI( NLV )
C
      RAD = 6.37E6
      RGAS = 287.05
      CP = 1005.
      CAPPA = RGAS / CP
C
      DO 1 LE=1,NLV
      DO 1 LO=1,NLOGG
      DTDTA(LO,LE) = UG(LO,LE)*TG(LO,LE)*RCL/RAD
      DTDTB(LO,LE) = -VG(LO,LE)*TG(LO,LE)*RCL/RAD
c     DTDTC(LO,LE) = -TG(LO,LE)*DG(LO,LE)  - CAPPA*TG(LO,LE)*
c    *  (UG(LO,LE)*DLAM(LO)+VG(LO,LE)*DPHI(LO))*RCL
      DTDTC(LO,LE) = -TG(LO,LE)*DG(LO,LE)  + CAPPA*TG(LO,LE)*
     |  dG(LO,LE)
  1   CONTINUE
      DO 2 LE=1,NLV
      DO 2 LO=1,NLOGG
C
       IF(LE.EQ.1) THEN
      DOTM = 0.
      DTM = 0.
      DOT = SG(LO,1)
      DTP = TG(LO,2)/RPI(1) - TG(LO,1)
       END IF
       IF(LE.EQ.NLV) THEN
      DOTM = SG(LO,NLV-1)
      DTM  = TG(LO,NLV) - RPI(NLV-1)*TG(LO,NLV-1)
      DOT  = 0.
      DTP  = 0.
       END IF
       IF(LE.GT.1.AND.LE.LT.NLV) THEN
      DOTM = SG(LO,LE-1)
      DTM  = TG(LO,LE) - RPI(LE-1)*TG(LO,LE-1)
      DOT  = SG(LO,LE)
      DTP  = TG(LO,LE+1)/RPI(LE) - TG(LO,LE)
       END IF
C
      DTDTC(LO,LE) = DTDTC(LO,LE) +(DOT*DTP+DOTM*DTM)/(2.*DEL(LE))
  2   CONTINUE
      DO 4 LE=1, NLV
      DO 4 LO=1, NLOGG
      IF(LE.EQ.1) THEN
        DOTM = 0.
         ELSE
        DOTM = SG( LO, LE-1 )
      END IF
      IF(LE.EQ.NLV) THEN
        DOTP = 0.
         ELSE
        DOTP = SG( LO, LE )
      END IF
      dtdtc(LO,LE) = dtdtc(LO,LE) +cappa*tg(lo,le)*(DOTP-DOTM)/DEL(LE)
4     CONTINUE
      RETURN
      END
*DECK DTEND
      SUBROUTINE DTEND (ZEG,UG,VG,DLAM,DPHI,SG,TG,
     *            DDDTA,DDDTB,DDDTC,DEL,RCL,NLOGG,NLV)
      DIMENSION
     *  ZEG( NLOGG , NLV ), UG( NLOGG , NLV ),  VG( NLOGG , NLV ),
     *  DLAM( NLOGG ),      DPHI( NLOGG ),      SG( NLOGG , NLV ),
     *  TG( NLOGG, NLV ),
     *  DDDTA( NLOGG, NLV ),DDDTB( NLOGG, NLV ),DDDTC(NLOGG,NLV )
      DIMENSION  DEL( NLV )
C
      RAD = 6.37E6
      RGAS = 287.05
C
      DO 1 LE=1,NLV
      DO 1 LO=1,NLOGG
      DDDTA(LO,LE) = -ZEG(LO,LE)*VG(LO,LE)*RCL/RAD
      DDDTA(LO,LE) = DDDTA(LO,LE) + RGAS*TG(LO,LE)*DLAM(LO)*RCL/RAD
      DDDTB(LO,LE) = -ZEG(LO,LE)*UG(LO,LE)*RCL/RAD
      DDDTB(LO,LE) = DDDTB(LO,LE) - RGAS*TG(LO,LE)*DPHI(LO)*RCL/RAD
      DDDTC(LO,LE) = (UG(LO,LE)**2 + VG(LO,LE)**2)*RCL/(2.*RAD**2)
  1   CONTINUE
C
      DO 2 LE=1,NLV
      DO 2 LO=1,NLOGG
       IF(LE.EQ.1) THEN
      DOTM = 0.
      DOT = SG(LO,1)
      DVDSM = 0.
      DUDSM = 0.
      DVDS  = VG(LO,2)-VG(LO,1)
      DUDS  = UG(LO,2)-UG(LO,1)
       END IF
       IF(LE.EQ.NLV) THEN
      DOTM = SG(LO,NLV-1)
      DOT  = 0.
      DVDSM= VG(LO,NLV)-VG(LO,NLV-1)
      DUDSM= UG(LO,NLV)-UG(LO,NLV-1)
      DVDS = 0.
      DUDS = 0.
       END IF
       IF(LE.GT.1.AND.LE.LT.NLV) THEN
      DOTM = SG(LO,LE-1)
      DOT  = SG(LO,LE)
      DVDSM= VG(LO,LE)-VG(LO,LE-1)
      DUDSM= UG(LO,LE)-UG(LO,LE-1)
      DVDS = VG(LO,LE+1)-VG(LO,LE)
      DUDS = UG(LO,LE+1)-UG(LO,LE)
       END IF
      DDDTA(LO,LE) = DDDTA(LO,LE) +(DOT*DUDS+DOTM*DUDSM)*
     *               RCL/(2.*DEL(LE)*RAD)
      DDDTB(LO,LE) = DDDTB(LO,LE) -(DOT*DVDS+DOTM*DVDSM)
     *              *RCL/(2.*DEL(LE)*RAD)
  2   CONTINUE
C
      RETURN
      END
*DECK VTEND
      SUBROUTINE VTEND (ZEG,UG,VG,DLAM,DPHI,SG,TG,
     *            DVDTA,DVDTB,DEL,RCL,NLOGG,NLV)
      DIMENSION
     *  ZEG( NLOGG , NLV ), UG( NLOGG , NLV ),  VG( NLOGG , NLV ),
     *  DLAM( NLOGG ),      DPHI( NLOGG ),      SG( NLOGG , NLV ),
     *  TG( NLOGG, NLV ),
     *  DVDTA( NLOGG, NLV ),DVDTB( NLOGG, NLV )
      DIMENSION  DEL( NLV )
C
      RAD = 6.37E6
      RGAS = 287.05
C
      DO 1 LE=1,NLV
      DO 1 LO=1,NLOGG
      DVDTA(LO,LE) = ZEG(LO,LE)*UG(LO,LE)*RCL/RAD
      DVDTA(LO,LE) = DVDTA(LO,LE) + RGAS*TG(LO,LE)*DPHI(LO)*RCL/RAD
      DVDTB(LO,LE) = -ZEG(LO,LE)*VG(LO,LE)*RCL/RAD
      DVDTB(LO,LE) = DVDTB(LO,LE) + RGAS*TG(LO,LE)*DLAM(LO)*RCL/RAD
  1   CONTINUE
C
      DO 2 LE=1,NLV
      DO 2 LO=1,NLOGG
       IF(LE.EQ.1) THEN
      DOTM = 0.
      DOT = SG(LO,1)
      DVDSM = 0.
      DUDSM = 0.
      DVDS  = VG(LO,2)-VG(LO,1)
      DUDS  = UG(LO,2)-UG(LO,1)
       END IF
       IF(LE.EQ.NLV) THEN
      DOTM = SG(LO,NLV-1)
      DOT  = 0.
      DVDSM= VG(LO,NLV)-VG(LO,NLV-1)
      DUDSM= UG(LO,NLV)-UG(LO,NLV-1)
      DVDS = 0.
      DUDS = 0.
       END IF
       IF(LE.GT.1.AND.LE.LT.NLV) THEN
      DOTM = SG(LO,LE-1)
      DOT  = SG(LO,LE)
      DVDSM= VG(LO,LE)-VG(LO,LE-1)
      DUDSM= UG(LO,LE)-UG(LO,LE-1)
      DVDS = VG(LO,LE+1)-VG(LO,LE)
      DUDS = UG(LO,LE+1)-UG(LO,LE)
       END IF
      DVDTA(LO,LE) = DVDTA(LO,LE) +(DOT*DVDS+DOTM*DVDSM)*
     *               RCL/(2.*DEL(LE)*RAD)
      DVDTB(LO,LE) = DVDTB(LO,LE) +(DOT*DUDS+DOTM*DUDSM)
     *              *RCL/(2.*DEL(LE)*RAD)
  2   CONTINUE
C
      RETURN
      END
*DECK ABSVOR
      SUBROUTINE ABSVOR(ZE,NWP1,NLV)
      COMPLEX ZE(NWP1,NWP1,NLV)
      TWOME = 2.*7.292E-5
      ABS = SQRT(2./3.)*TWOME
      DO 1 K=1,NLV
      ZE(1,2,K) = ZE(1,2,K) + ABS
  1   CONTINUE
      RETURN
      END
*DECK DELSQR
      SUBROUTINE DELSQR(A,B,NWP1,NLV)
      COMPLEX A(NWP1,NWP1,NLV),B(NWP1,NWP1,NLV)
      DO 1 K=1,NLV
      DO 1 N=1,NWP1
      DO 1 M=1,NWP1
      TN = FLOAT(M-1+N-1)
      B(M,N,K) = -TN*(TN+1.)*A(M,N,K)
  1   CONTINUE
      RETURN
      END
*DECK ALLOUT
      SUBROUTINE ALLOUT(NUNOUT,A,NX,NY,NLV,B,NW1)
c     COMPLEX A(NW1,NW1,NLV),B(NW1,NW1)
      COMPLEX A(NW1,NW1,NLV),B(NX,NY)
      DO 2 K=1,NLV
C
      DO 1 J=1,NY
      DO 1 I=1,NX
      B(I,J) = A(I,J,K)
   1  CONTINUE
      WRITE(NUNOUT) B
C
   2  CONTINUE
      RETURN
      END
*DECK READ85
      SUBROUTINE READ85 (NSFILE,FLDIN,IS,LEV,FIELD,NW1,LTRAN)
      DIMENSION BUFR(2,NW1,NW1),FLDIN(2,NW1,NW1)
      COMPLEX FIELD(NW1,NW1,LEV)
      LOGICAL LTRAN
       IRS=IS+1
       IRE=IS+LEV
       DO 1 K=IRS,IRE
       READ(NSFILE,rec=K) FLDIN

      do 2 j=1,NW1
      do 2 i=1,NW1
         BUFR(1,i,j)=FLDIN(1,i,j)
         BUFR(2,i,j)=FLDIN(2,i,j)
  2   CONTINUE

      CALL TRANIN (BUFR,1,FIELD(1,1,K-IS),NW1,LTRAN)
      
  1   CONTINUE
      
      RETURN
      END

*DECK RDWFLD
      SUBROUTINE RDWFLD (NSFILE,BUFR,NHARM,LEV,BWAVM,NW1,NX,NY)
      DIMENSION BUFR(2,NW1,NW1)
      DIMENSION BUFRIN(2,21,21)
      COMPLEX  BWAVM(NW1,NW1,LEV)
          do 5 j=1,NW1
          do 5 i=1,NW1
          do 5 k=1,2
             BUFR(k,i,j)=0.
  5   CONTINUE
          READ(NSFILE) BUFRIN
          do 1 j=1,NX
          do 1 i=1,NY
          do 1 k=1,2
             BUFR(k,i,j)=BUFRIN(k,i,j)
c            BUFR(k,i,j)=0.            
  1   CONTINUE
      CALL TRANSW (BUFR,LEV,BWAVM,NW1)
      RETURN
      END
*DECK TRANSW
      SUBROUTINE TRANSW (A,N,B,NW1)
      COMPLEX A(NW1,NW1,N), B(NW1,NW1,N)
      DO 5 K=1,N
      DO 8 J=1,NW1
      DO 8 M=1,NW1
      B(M,J,K) = A(J,M,K)
    8 CONTINUE
    5 CONTINUE
      RETURN
      END
*DECK FILLCM
        SUBROUTINE FILLCM (A,IDIM,B)
        COMPLEX A(IDIM),B(IDIM)
        DO 1 I = 1 , IDIM
        A(I) = B(I) 
 1      CONTINUE
        RETURN
        END
