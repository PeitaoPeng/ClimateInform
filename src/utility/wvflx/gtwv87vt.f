CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C    PROGRAM TO to get quantities for computing the Plumb wave activity    
C    fluxes(PTV is computed with U,V & T)
C    JINITZ = 1 IF UNINITIALIZED AND INITIALIZED FILES ARE WRITTEN     C
C               IN PRESSURE HISTORY FILES                              C
C    JINITZ = 0 IF UNINITIALIZED AND INITIALIZED FILES ARE NOT WRITTEN C
C               IN PRESSURE HISTORY FILES                              C
C                                                                      C
C    CURRENTLY SET UP SO THAT INITIAL (UNINITIALIZED AND INITIALIZED)  C
C    FIELDS ARE SKIPPED, IF THEY ARE WRITTEN ON THE PRESSURE HISTORY   C
C    FILES (THAT IS, IF JINITZ=1)                                      C
C                                                                      C
C    INPUT UNIT NUMBERS ARE 61,62,63,..  (PRESSURE HISTORY FILES)      C
C    FULL SEASONAL AVERAGE ON UNIT 40    (STORED ON CFS)               C
C                                                                      C
C    LDRCT DETERMINES IF A PARTICULAR FIELD IS TO BE AVERAGED          C
C             LDRCT(1)     REFERS TO SURFACE PRESSURE                  C
C             LDRCT(2 -8)  REFER  TO  7 LEVELS OF U-VELOCITY           C
C             LDRCT(9-15)  REFER  TO  7 LEVELS OF V-VELOCITY           C
C             LDRCT(16-22) REFER  TO  7 LEVELS OF OMEGA                C
C             LDRCT(23)    REFERS TO SEA-LEVEL PRESSURE                C
C             LDRCT(24-30) REFER  TO  7 LEVELS OF GEOPOTENTIAL HEIGHT  C
C             LDRCT(31-37) REFER  TO  7 LEVELS OF TEMPERATURE          C
C             LDRCT(38-44) REFER  TO  7 LEVELS OF SPECIFIC HUMIDITY    C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C     CONFIG.2                                                         C
C       "REDUCED SET" OF VARIABLES (DOES NOT INCLUDE HEATING)          C
C     CONFIG.2H                                                        C
C       "REDUCED SET" OF VARIABLES (INCLUDES DIABATIC HEATING)         C
C                                                                      C
C (1)  (DAILY) SURFACE PRESSURE                                        C
C (2-8)        U VELOCITY                     (7 LEVELS)               C
C (9-15)       V VELOCITY                     (7 LEVELS)               C
C (16-22)      OMEGA                          (7 LEVELS)               C
C (23)         SEA LEVEL PRESSURE                                      C
C (24-30)      GEOPOTENTIAL HEIGHT            (7 LEVELS)               C
C (31-37)      ABSOLUTE TEMPERATURE           (7 LEVELS)               C
C (38-44)      SPECIFIC HUMIDITY              (7 LEVELS)               C
C                                                                      C
C (45-51)      DIABATIC HEATING               (7 LEVELS)               C
C                                                                      C
C  NLEV (=7) LEVELS ON CONFIG. 2 HISTORY FILES:                        C
C  1000,850,700,500,300,200,100                                        C
C                                                                      C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C   parameters need to be changed for different purpose of use are:  C
C   JINITZ......should be 0 except for January                         C
C   LDRCT(XX)                                                          C
C   FHRBEG
C   FHREND
C   NEDDY
C   NTIME
C   NFCUT
C   NFBS NFBE
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C   Needed Subroutings Are in Source Files:
C          fft.f  fftgcm.f  genpnme.f   glats.f
C
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      PARAMETER(NW=40)
      PARAMETER(NLEG=NW+1)
      PARAMETER(NM=NW)
      PARAMETER(NMP=NW+1)
      PARAMETER(NLONG=128)
      PARAMETER(NLATG=102)
      PARAMETER(NMAND=7,MAND=NMAND-2)
      PARAMETER(NSKPIN=44,NFIELD=44)
CC    PARAMETER(NSKPIN=44,NFIELD=51)
      PARAMETER(NAVFLD=NFIELD,JINITZ=0)
      PARAMETER(NEDDY=3*NMAND,NTIME=90)              
      PARAMETER(NWAVE=2*NTIME+15)
      PARAMETER(NFCUT=14)
      PARAMETER(NLEGP=NLEG+1)
      PARAMETER(NFBS=1,NFBE=1)
      PARAMETER(NLONGP=NLONG+1,IMAXT=3*NLONG/2)
      PARAMETER(KHALF=(NLATG+1)/2)
      LOGICAL LDRCT
      DIMENSION LDRCT(NFIELD)
      COMMON/GAUSS/RADG(NLATG),GWGT(NLATG)
      COMMON/POLY1/PLEG(NLEG,NMP,NLATG),DPLEG(NLEG,NMP,NLATG)
      COMMON/FFTAR1/TRIGS(IMAXT),IFAX(10)
      COMMON/FFTAR2/ARRAY(NLONGP,NLATG),WORK(NLONGP,NLATG)
      COMMON/EPSLON/EPS(NLEG,NMP),XXN(NLEG,NMP),XXM(NMP)
      COMMON/CORCOS/CORIOLIS(NLATG),PLEVEL(NMAND)  
      COMMON/AVEARY/                   AVGSFP(2,NLEG,NMP      ),
     1        AVGUVL(2,NLEG,NMP,NMAND),AVGVVL(2,NLEG,NMP,NMAND),
     2        AVGOMG(2,NLEG,NMP,NMAND),
     3        AVGSLP(2,NLEG,NMP      ),
     4        AVGGEO(2,NLEG,NMP,NMAND),
     5        AVGTMP(2,NLEG,NMP,NMAND),AVGSPH(2,NLEG,NMP,NMAND)
      COMMON/PTV/EDYPTV(NLONG,NLATG,MAND),EDYGDT(NLONG,NLATG,NMAND),
     1           AVGPTT(NLONG,NLATG,NMAND),EDYGRV(NLONG,NLATG,MAND),
     2           AVDPTT(NLONG,NLATG,NMAND),GLBAVT(NMAND)
      DIMENSION AVGGRD(NLONG,NLATG,NAVFLD)
      DIMENSION WORKGD(NLONG,NLATG,NMAND)
      DIMENSION AVGSPC(2,NLEG,NMP,NAVFLD)
      DIMENSION COLRAD(KHALF),WGT(KHALF),WGTCS(KHALF),RCS2(KHALF)
      DIMENSION FIELDG(NLONG,NLATG)
      DIMENSION FLDSPC(2,NLEG,NMP)
      DIMENSION IDATE(4)
      DIMENSION COSLAT(NLATG)  
      DIMENSION EDDYGD(NLONG,NLATG,NEDDY)
      DIMENSION EDDYSP(2,NLEG,NMP,NEDDY)
      DIMENSION EDYHST(2,NLEG,NMP,NEDDY,NTIME)
      DIMENSION EDYSPU(2,NLEG,NMP,MAND),EDYSPV(2,NLEG,NMP,MAND)
      DIMENSION EDYGDU(NLONG,NLATG,MAND),EDYGDV(NLONG,NLATG,MAND)
      DIMENSION EDYSRV(2,NLEG,NMP,MAND)
      DIMENSION AVSPRV(2,NLEG,NMP,MAND),AVGDRV(NLONG,NLATG,MAND)
      DIMENSION AVEDUU(NLONG,NLATG,MAND),AVEDVV(NLONG,NLATG,MAND)
      DIMENSION AVEDUV(NLONG,NLATG,MAND),AVEDUT(NLONG,NLATG,MAND)
      DIMENSION AVEDVT(NLONG,NLATG,MAND),AVEDTT(NLONG,NLATG,MAND)
      DIMENSION AVEDES(NLONG,NLATG,MAND)                          
      DIMENSION AVEDUQ(NLONG,NLATG,MAND),AVEDVQ(NLONG,NLATG,MAND)
      DIMENSION POOVRP(NMAND),TOTVEL(NLONG,NLATG,14)
      DIMENSION RIN(NTIME),ROUT(NTIME),WSAVE(NWAVE)
      EQUIVALENCE(AVGSFP(1,1,1),AVGSPC(1,1,1,1))
      DATA FHRBEG/7992/
      DATA FHREND/10128/
      DATA PLEVEL/100000.,85000.,70000.,50000.,30000.,20000.,10000./
      PI=ACOS(-1.0)
      GRAVITY=9.8
      LENS1=2*NLEG*NMP
      LENS2=2*NLEG*NMP*NMAND
      LENG1=NLONG*NLATG
      LENG2=NLONG*NLATG*NMAND
      LENG3=NLONG*NLATG*MAND
      LENG4=NLONG*NLATG*3
      R=6.37E+06
      RR=R**2
      UNDEF=9.99E+55
      DO 10 JJ=1,NFIELD
      LDRCT(JJ)=.FALSE.
   10 CONTINUE
      DO 15 II=2,15 
      LDRCT(II)=.TRUE.
   15 CONTINUE
      DO 25 II=31,37 
      LDRCT(II)=.TRUE.
   25 CONTINUE
      CALL GLATS(KHALF,COLRAD,WGT,WGTCS,RCS2)
      DO 30 K=1,KHALF
      RADG(K)=PI/2.0 - COLRAD(K)
      RADG(NLATG-K+1)=-RADG(K)
      GWGT(K)=WGT(K)
      GWGT(NLATG-K+1)=+GWGT(K)
   30 CONTINUE
      DO 40 JG=1,NLATG
      CALL GENPNME(RADG(JG),PLEG(1,1,JG),DPLEG(1,1,JG))
   40 CONTINUE
C.....Compute the Coriolis Parameter
      OMEGA=2*PI/(24.*3600.)
      DO 35 K=1,NLATG
         CORIOLIS(K)=2*OMEGA*SIN(RADG(K))
C        CORIOLIS(K)=2*OMEGA*SIN(PI/4.)
         COSLAT(K)=COS(RADG(K))
   35 CONTINUE
C.....Compute the (po/p)**xkapa        
      GASCON=287.
      CP=1004.
      XKAPA=287./1004.       
      DO 45 K=1,NMAND
         POOVRP(K)=(PLEVEL(1)/PLEVEL(K))**XKAPA
   45 CONTINUE
      DO 50 IP=1,NAVFLD
      DO 50 J=1,NLATG
      DO 50 I=1,NLONG
      AVGGRD(I,J,IP)=0.0
   50 CONTINUE
      IREAD=0
      IUNIT=61
      IF(JINITZ .EQ. 1) THEN
C.....UNINITIALIZED AND INITIALIZED FIELDS
         READ(IUNIT) FHOUR,IDATE
         WRITE(6,7000) FHOUR,IDATE
 7000    FORMAT(' FHOUR=',F10.2,' IDATE=',4I4,' UNINITIALIZED FIELDS ')
         DO 100 IP=1,NSKPIN
         READ(IUNIT)
  100    CONTINUE
         READ(IUNIT) FHOUR,IDATE
         WRITE(6,7010) FHOUR,IDATE
 7010    FORMAT(' FHOUR=',F10.2,' IDATE=',4I4,'   INITIALIZED FIELDS ')
         DO 120 IP=1,NSKPIN
         READ(IUNIT)
  120    CONTINUE
      END IF
C===== REFERENCE POINT FROM WHICH PROGRAM READS NEW TIME RECORD =====
  123 CONTINUE
      READ(IUNIT,END=130) FHOUR,IDATE
      GO TO 132
  130 IUNIT=IUNIT+1
      GO TO 123
  132 CONTINUE
      IF(FHOUR-FHRBEG .LE. -.01) THEN
C.....TIME TOO EARLY, SKIP DATA
         DO 140 IP=1,NFIELD
         READ(IUNIT)
  140    CONTINUE
         WRITE(6,7020) FHOUR,IDATE
 7020    FORMAT(' FHOUR=',F10.2,' IDATE=',4I4,'   TIME TOO EARLY     ')
         GO TO 123
      END IF
      IREAD=IREAD+1
C.....READ INDIVIDUAL FIELDS AND UPDATE ACCUMULATION
      DO 205 IFLD=1,NFIELD
      IF(LDRCT(IFLD)) THEN
         READ(IUNIT) FIELDG
         CALL XUPDTG(FIELDG,AVGGRD(1,1,IFLD),NLONG,NLATG)
      ELSE
         READ(IUNIT)
      END IF
  205 CONTINUE
C==== END OF ACCUMULATIONS ===================
      IF(FHOUR-FHREND .LE. -.01) THEN
          WRITE(6,7200) FHOUR,IREAD
 7200     FORMAT(' DATA PROCESSED FOR FHOUR',F10.1,
     1     'IREAD= ',I4)
          GO TO 123
      ELSE
          WRITE(6,7200) FHOUR,IREAD
          GO TO 600
      END IF
  600 CONTINUE
C=====DIVIDE BY NUMBER OF READS, TRANSFORM AND WRITE DATA ======
      XREAD=IREAD
      DO 620 IG=1,NAVFLD
      DO 620 J=1,NLATG
      DO 620 I=1,NLONG
      AVGGRD(I,J,IG)=AVGGRD(I,J,IG)/XREAD
  620 CONTINUE
C.....write out average U             
      JUNIT=40
      DO 655 K=1,NMAND
      DO 655 J=1,NLATG
      DO 655 I=1,NLONG
         WORKGD(I,J,K)=AVGGRD(I,J,K+1)
  655 CONTINUE
      CALL XWRITE(WORKGD,JUNIT,LENG2)
C.....write out average V             
      DO 665 K=1,NMAND
      DO 665 J=1,NLATG
      DO 665 I=1,NLONG
         WORKGD(I,J,K)=AVGGRD(I,J,K+8)
  665 CONTINUE
      CALL XWRITE(WORKGD,JUNIT,LENG2)
C.....transform absolute tamp to potential temp         
      DO 630 IG=31,37
      DO 630 J=1,NLATG
      DO 630 I=1,NLONG
      AVGGRD(I,J,IG)=AVGGRD(I,J,IG)*POOVRP(IG-30) 
      AVGPTT(I,J,IG-30)=AVGGRD(I,J,IG)
  630 CONTINUE
C.....compute global mean potential temperature
      DO 635 K=1,NMAND
      CALL AVERAGE(AVGPTT(1,1,K),GLBAVT(K),39)
  635 CONTINUE
C.....write out average potential temperature
      CALL XWRITE(AVGPTT,JUNIT,LENG2)
C.....TRANSFORM FIELDS TO SPECTRAL SPACE
      DO 640 J=1,NAVFLD
         CALL TRNSFM(AVGGRD(1,1,J),AVGSPC(1,1,1,J))
  640 CONTINUE
CC
CC====compute eddy by subtracting mean from daily data
CC
C.....only want eddy u & v and potential temperature
      IREAD=0
      IUNIT=61
      REWIND IUNIT
      IF(JINITZ .EQ. 1) THEN
C.....UNINITIALIZED AND INITIALIZED FIELDS
         READ(IUNIT) FHOUR,IDATE
         WRITE(6,7000) FHOUR,IDATE
         DO 200 IP=1,NSKPIN
         READ(IUNIT)
  200    CONTINUE
         READ(IUNIT) FHOUR,IDATE
         WRITE(6,7010) FHOUR,IDATE
         DO 220 IP=1,NSKPIN
         READ(IUNIT)
  220    CONTINUE
      END IF
C===== REFERENCE POINT FROM WHICH PROGRAM READS NEW TIME RECORD =====
  223 CONTINUE
      READ(IUNIT,END=230) FHOUR,IDATE
      GO TO 232
  230 IUNIT=IUNIT+1
      REWIND IUNIT
      GO TO 223
  232 CONTINUE
      IF(FHOUR-FHRBEG .LE. -.01) THEN
C.....TIME TOO EARLY, SKIP DATA
         DO 240 IP=1,NFIELD
         READ(IUNIT)
  240    CONTINUE
         WRITE(6,7020) FHOUR,IDATE
         GO TO 223
      END IF
      IREAD=IREAD+1
C.....READ INDIVIDUAL FIELDS 
      K=0 
      DO 400 IFLD=1,NFIELD
      IF(LDRCT(IFLD)) THEN
         K=K+1
         READ(IUNIT) FIELDG
         DO 420 J=1,NLATG
         DO 420 I=1,NLONG
         EDDYGD(I,J,K)=FIELDG(I,J)
  420    CONTINUE
      ELSE
         READ(IUNIT)
      END IF
  400 CONTINUE
C.....transform absolute temp to potential temp         
      DO 440 IG=15,NEDDY
      DO 440 J=1,NLATG
      DO 440 I=1,NLONG
      EDDYGD(I,J,IG)=EDDYGD(I,J,IG)*POOVRP(IG-14) 
  440 CONTINUE
c.....compute eddy u & v                      
      DO 450 K=1,14   
      DO 450 J=1,NLATG
      DO 450 I=1,NLONG
      TOTVEL(I,J,K)=EDDYGD(I,J,K)-AVGGRD(I,J,1+K)
  450 CONTINUE
C.....have the rotational part of Velocity
      DO 452 K=1,7
      CALL RVDVVPST(TOTVEL(1,1,K),TOTVEL(1,1,K+7),
     1              EDDYGD(1,1,K),EDDYGD(1,1,K+7))
  452 CONTINUE
C.....compute the eddy potential temp
      DO 455 K=15,NEDDY
      DO 455 J=1,NLATG
      DO 455 I=1,NLONG
      EDDYGD(I,J,K)=EDDYGD(I,J,K)-AVGGRD(I,J,16+K)
  455 CONTINUE
C.....TRANSFORM FIELDS TO SPECTRAL SPACE
      DO 460 J=1,NEDDY
         CALL TRNSFM(EDDYGD(1,1,J),EDDYSP(1,1,1,J))
  460 CONTINUE
C.....write EDDYSP to the array EDYHST
      DO 470 L=1,NEDDY
      DO 470 K=1,NMP 
      DO 470 J=1,NLEG
      DO 470 I=1,2
         EDYHST(I,J,K,L,IREAD)=EDDYSP(I,J,K,L)
  470 CONTINUE
C
C==== END OF getting eddy at this time step=
C
      IF(FHOUR-FHREND .LE. -.01) THEN
          WRITE(6,7200) FHOUR,IREAD
          GO TO 223
      ELSE
          WRITE(6,7200) FHOUR,IREAD
          GO TO 660
      END IF
  660 CONTINUE
      
CC
CC====filter out the variation of uninteresting time scales
CC
      CALL RFFTI(NTIME,WSAVE)
      DO 480 L=1,NEDDY
      DO 480 K=1,NMP
      DO 480 J=1,NLEG
      DO 480 I=1,2    
      DO 490 NT=1,NTIME
         RIN(NT)=EDYHST(I,J,K,L,NT)
  490 CONTINUE
C     CALL FILTERBP(NTIME,RIN,ROUT,NFBS,NFBE,WSAVE)
C     CALL FILTERLP(NTIME,RIN,ROUT,NFCUT,WSAVE)
      CALL FILTERHP(NTIME,RIN,ROUT,NFCUT,WSAVE)
      DO 500 NT=1,NTIME
         EDYHST(I,J,K,L,NT)=ROUT(NT)
  500 CONTINUE
  480 CONTINUE
C
CC====compute the u, v & potential T in spectral spc
C
      R=6.37E+06
      XTIME=FLOAT(NTIME)
      DO 505 K=1,MAND
      DO 505 J=1,NLATG
      DO 505 I=1,NLONG
      AVEDUU(I,J,K)=0.0
      AVEDVV(I,J,K)=0.0
      AVEDUV(I,J,K)=0.0
      AVEDUT(I,J,K)=0.0
      AVEDVT(I,J,K)=0.0
      AVEDTT(I,J,K)=0.0
      AVEDES(I,J,K)=0.0
      AVEDUQ(I,J,K)=0.0
      AVEDVQ(I,J,K)=0.0
  505 CONTINUE
      DO 510 NT=1,NTIME
CC....compute  spectral eddy U & V
      DO 515 L=1,MAND
      DO 515 K=1,NMP
      DO 515 J=1,NLEG
      DO 515 I=1,2
      EDYSPU(I,J,K,L)=EDYHST(I,J,K,L+1,NT)
      EDYSPV(I,J,K,L)=EDYHST(I,J,K,L+8,NT)
  515 CONTINUE
C 
CC====transform u & v & T to Gaussian grid    
C 
      DO 535 L=1,MAND
         CALL EVLSCA(EDYSPU(1,1,1,L),EDYGDU(1,1,L))
         CALL EVLSCA(EDYSPV(1,1,1,L),EDYGDV(1,1,L))
  535 CONTINUE
      DO 540 L=1,NMAND
         CALL EVLSCA(EDYHST(1,1,1,L+14,NT),EDYGDT(1,1,L))
  540 CONTINUE
C
CC====Compute average eddy fluxes for latter use
C
      DO 545 L=1,MAND
      DO 550 J=1,NLATG
      DO 550 I=1,NLONG
         EDDYGD(I,J,1)=EDYGDU(I,J,L)*EDYGDU(I,J,L)/XTIME
         EDDYGD(I,J,2)=EDYGDV(I,J,L)*EDYGDV(I,J,L)/XTIME
         EDDYGD(I,J,3)=EDYGDU(I,J,L)*EDYGDV(I,J,L)/XTIME
         EDDYGD(I,J,4)=EDYGDU(I,J,L)*EDYGDT(I,J,L+1)/XTIME
         EDDYGD(I,J,5)=EDYGDV(I,J,L)*EDYGDT(I,J,L+1)/XTIME
         EDDYGD(I,J,6)=EDYGDT(I,J,L+1)*EDYGDT(I,J,L+1)/XTIME
  550 CONTINUE
         CALL XUPDTG(EDDYGD(1,1,1),AVEDUU(1,1,L),NLONG,NLATG)
         CALL XUPDTG(EDDYGD(1,1,2),AVEDVV(1,1,L),NLONG,NLATG)
         CALL XUPDTG(EDDYGD(1,1,3),AVEDUV(1,1,L),NLONG,NLATG)
         CALL XUPDTG(EDDYGD(1,1,4),AVEDUT(1,1,L),NLONG,NLATG)
         CALL XUPDTG(EDDYGD(1,1,5),AVEDVT(1,1,L),NLONG,NLATG)
         CALL XUPDTG(EDDYGD(1,1,6),AVEDTT(1,1,L),NLONG,NLATG)
  545 CONTINUE
C
CC====compute eddy potential vorticity q'               
C
      CALL UVPTV(EDYGDU,EDYGDV)
C
CC====compute average eddy enstrophy , u'q' and v'q'      
C
      DO 580 L=1,MAND
      DO 585 J=1,NLATG
      DO 585 I=1,NLONG
         EDDYGD(I,J,1)=0.5*EDYPTV(I,J,L)*EDYPTV(I,J,L)/XTIME
         EDDYGD(I,J,2)=EDYGDU(I,J,L)*EDYPTV(I,J,L)/XTIME
         EDDYGD(I,J,3)=EDYGDV(I,J,L)*EDYPTV(I,J,L)/XTIME
  585 CONTINUE
         CALL XUPDTG(EDDYGD(1,1,1),AVEDES(1,1,L),NLONG,NLATG)
         CALL XUPDTG(EDDYGD(1,1,2),AVEDUQ(1,1,L),NLONG,NLATG)
         CALL XUPDTG(EDDYGD(1,1,3),AVEDVQ(1,1,L),NLONG,NLATG)
  580 CONTINUE
  510 CONTINUE
C
CC====write out more selected quantities
C
C.....eddy U               
      CALL XWRITE(EDYGDU,JUNIT,LENG3)
C.....eddy V               
      CALL XWRITE(EDYGDV,JUNIT,LENG3)
C.....avg eddy fluxes
      CALL XWRITE(AVEDUU,JUNIT,LENG3)
      CALL XWRITE(AVEDVV,JUNIT,LENG3)
      CALL XWRITE(AVEDUV,JUNIT,LENG3)
      CALL XWRITE(AVEDUT,JUNIT,LENG3)
      CALL XWRITE(AVEDVT,JUNIT,LENG3)
      CALL XWRITE(AVEDTT,JUNIT,LENG3)
C.....avg eddy enstrophy
      CALL XWRITE(AVEDES,JUNIT,LENG3)
C.....eddy RV          
      CALL XWRITE(EDYGRV,JUNIT,LENG3)
C.....eddy PTV             
      CALL XWRITE(EDYPTV,JUNIT,LENG3)
C.....average eddy uq & vq
      CALL XWRITE(AVEDUQ,JUNIT,LENG3)
      CALL XWRITE(AVEDVQ,JUNIT,LENG3)
      STOP
      END
 
      SUBROUTINE XWRITE(ARRAY,JUNIT,N)
      DIMENSION ARRAY(N)
      WRITE(JUNIT) ARRAY
      RETURN
      END

      SUBROUTINE TRNSFM(FIELDG,FLDSPC)
      PARAMETER(NW=40)
      PARAMETER(NLEG=NW+1)
      PARAMETER(NM=NW)
      PARAMETER(NMP=NW+1)
      PARAMETER(NLONG=128)
      PARAMETER(NLATG=102)
      PARAMETER(NLONGP=NLONG+1,IMAXT=3*NLONG/2)
      PARAMETER(KHALF=(NLATG+1)/2)
      COMMON/GAUSS/RADG(NLATG),GWGT(NLATG)
      COMMON/POLY1/PLEG(NLEG,NMP,NLATG),DPLEG(NLEG,NMP,NLATG)
      COMMON/FFTAR1/TRIGS(IMAXT),IFAX(10)
      COMMON/FFTAR2/ARRAY(NLONGP,NLATG),WORK(NLONGP,NLATG)
      DIMENSION FIELDG(NLONG,NLATG)
      DIMENSION FLDSPC(2,NLEG,NMP)
      DO 20 IM=1,NMP
      DO 20 IN=1,NLEG
      DO 20 IC=1,2
      FLDSPC(IC,IN,IM)=0.0
   20 CONTINUE
      DO 60 JG=1,NLATG
      DO 40 IG=1,NLONG
      ARRAY(IG,JG)=FIELDG(IG,JG)
   40 CONTINUE
      ARRAY(NLONGP,JG)=0.0
   60 CONTINUE
      ISIGN=-1
      INC=1
      JUMP=NLONGP
      LOT=NLATG
      CALL FFT991(ARRAY,WORK,TRIGS,IFAX,INC,JUMP,NLONG,LOT,ISIGN)
      DO 200 JG=1,NLATG
      DO 180 IM=1,NMP
      DO 180 IN=1,NLEG
      FLDSPC(1,IN,IM)=FLDSPC(1,IN,IM)+ARRAY(2*IM-1,JG)*PLEG(IN,IM,JG)*
     1 GWGT(JG)
      FLDSPC(2,IN,IM)=FLDSPC(2,IN,IM)+ARRAY(2*IM  ,JG)*PLEG(IN,IM,JG)*
     1 GWGT(JG)
  180 CONTINUE
  200 CONTINUE
      RETURN
      END


 
      SUBROUTINE XUPDTG(FIELD,AVE,NLON,NLAT)
      DIMENSION FIELD(NLON,NLAT),AVE(NLON,NLAT)
      DO 100 J=1,NLAT
      DO 100 I=1,NLON
      AVE(I,J)=AVE(I,J)+FIELD(I,J)
  100 CONTINUE
      RETURN
      END
 
      SUBROUTINE FORWRT(FLDSPC,N,JUNIT)
      DIMENSION FLDSPC(N)
      WRITE(JUNIT,7000) FLDSPC
 7000 FORMAT(5(E16.8))
      RETURN
      END
 
 
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C  band pass filter                                                C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      subroutine FILTERBP(n,rin,rout,nfs,nfe,wsave)
      dimension rin(1),rout(1),wsave(1)
      call RFFTF(n,rin,wsave)
      x=float(n)
      nfss=2*nfs+1
      nfee=2*nfe+1
      do 10 i=1,nfss-1
         rout(i)=0.
10    continue
      do 20 i=nfss,nfee
        rout(i)=rin(i)/x
20    continue
      do 30 i=nfe+1,n
        rout(i)=0.
30    continue
      call RFFTB(n,rout,wsave)
      return
      end
 

CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C  low pass filter                                                C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      subroutine FILTERLP(n,rin,rout,nf,wsave)
      dimension rin(1),rout(1),wsave(1)
      call RFFTF(n,rin,wsave)
      x=float(n)
      nff=2*nf+1
      do 20 i=1,nff-2
        rout(i)=rin(i)/x
20    continue
      do 30 i=nff-1,n
        rout(i)=0.
30    continue
      rout(1)=0
      call RFFTB(n,rout,wsave)
      return
      end


CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C  high pass filter                                                C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      subroutine FILTERHP(n,rin,rout,nf,wsave)
      dimension rin(1),rout(1),wsave(1)
      call RFFTF(n,rin,wsave)
      x=float(n)
      nff=2*nf+1
      do 30 i=1,nff
        rout(i)=0.
30    continue
      do 20 i=nff+1,n
        rout(i)=rin(i)/x
20    continue
      call RFFTB(n,rout,wsave)
      return
      end


CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C  transform from spctral to Gaussian grid                        C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      SUBROUTINE EVLSCA(ARRS,ARRP)
      PARAMETER(NW=40)
      PARAMETER(MNWV2=2*(NW+1)*(NW+1))
      PARAMETER(NLEG=NW+1,NM=NW,NMP=NM+1)
      PARAMETER(NLATG=102,NLONG=128,NLONGP=NLONG+1,IMAXT=3*NLONG/2)
      COMMON/POLY1/PLEG(NLEG,NMP,NLATG),DPLEG(NLEG,NMP,NLATG)
      COMMON/FFTAR1/TRIGS(IMAXT),IFAX(10)
      COMMON/FFTAR2/ARRAY(NLONGP,NLATG),WORK(NLONGP,NLATG)
      DIMENSION ARRS(2,NLEG,NMP),ARRP(NLONG,NLATG)
      IM1=2*NMP+1
      IM2=NLONGP
      DO 200 J=1,NLATG
      DO 120 IM=1,NMP
      DO 120 IC=1,2
      YSUM=0.0
      DO 100 N=1,NLEG
      YSUM=YSUM+ARRS(IC,N,IM)*PLEG(N,IM,J)
  100 CONTINUE
      ARRAY((IM-1)*2+IC,J)=YSUM
  120 CONTINUE
      DO 130 IM=IM1,IM2
      ARRAY(IM,J)=0.0
  130 CONTINUE
  200 CONTINUE
      INC=1
      ISIGN=+1
      JUMP=NLONGP
      LOT=NLATG
      CALL FFT991(ARRAY,WORK,TRIGS,IFAX,INC,JUMP,NLONG,LOT,ISIGN)
      DO 240 J=1,NLATG
      DO 240 I=1,NLONG
      ARRP(I,J)=ARRAY(I,J)
  240 CONTINUE
      RETURN
      END
 


CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C  compute velocity UCOS(LAT) & VCOS(LAT) in spectral space
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      SUBROUTINE VELOSP(FILDIN,FILDUU,FILDVV)
      PARAMETER(NW=40,NLEG=NW+1,NMP=NW+1)
      COMMON/EPSLON/EPS(NLEG,NMP),XXN(NLEG,NMP),XXM(NMP)
      DIMENSION FILDIN(2,NLEG,NMP)                   
      DIMENSION FILDUU(2,NLEG,NMP),FILDVV(2,NLEG,NMP)
      R=6.371E+06
CC....compute  spectral U  
      DO 100 I=1,2
      DO 100 K=1,NMP
         DO 200 J=2,NW  
      FILDUU(I,J,K)=((XXN(J,K)-1)*EPS(J,K)*FILDIN(I,J-1,K)-
     1(XXN(J,K)+2)*EPS(J+1,K)*FILDIN(I,J+1,K))/R
  200 CONTINUE
         FILDUU(I,1,K)=-(XXN(1,K)+2)*EPS(2,K)*FILDIN(I,2,K)
     1/R
         FILDUU(I,NLEG,K)=(XXN(NLEG,K)-1)*EPS(NLEG,K)*
     1FILDIN(I,NW,K)/R
  100 CONTINUE
CC....compute  spectral V  
      DO 300 K=1,NMP
      DO 300 J=1,NLEG 
         FILDVV(1,J,K)=-XXM(K)*FILDIN(2,J,K)/R
         FILDVV(2,J,K)=XXM(K)*FILDIN(1,J,K)/R
  300 CONTINUE
      RETURN
      END

      
                   


CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C     compute  potential vorticity with relative vort from U & V
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      SUBROUTINE UVPTV(UWN,VWN)
      PARAMETER(NW=40,NLEG=NW+1,NMP=NW+1)
      PARAMETER(NLATG=102,NLONG=128,NMAND=7,MAND=NMAND-2)
      COMMON/EPSLON/EPS(NLEG,NMP),XXN(NLEG,NMP),XXM(NMP)
      COMMON/CORCOS/CORIOLIS(NLATG),PLEVEL(NMAND)  
      DIMENSION UWN(NLONG,NLATG,MAND),VWN(NLONG,NLATG,MAND)             
      DIMENSION AVSPRV(2,NLEG,NMP,MAND) 
      COMMON/PTV/EDYPTV(NLONG,NLATG,MAND),EDYGDT(NLONG,NLATG,NMAND),
     1           AVGPTT(NLONG,NLATG,NMAND),EDYGRV(NLONG,NLATG,MAND),
     2           AVDPTT(NLONG,NLATG,NMAND),GLBAVT(NMAND)
      GASCON=287.
      XKAPA=287./1004.       
      R=6.371E+06
      RR=R*R
C.....have relative vort
      DO 100 L=1,MAND
      CALL RELVOT(UWN(1,1,L),VWN(1,1,L),AVSPRV(1,1,1,L))
C     CALL RLVORT(UWN(1,1,L),VWN(1,1,L),EDYGRV(1,1,L))
  100 CONTINUE
      DO 720 L=1,MAND
         CALL EVLSCA(AVSPRV(1,1,1,L),EDYGRV(1,1,L))
  720 CONTINUE
      DO 725 K=1,MAND
      DO 725 J=1,NLATG
      DO 725 I=1,NLONG
      FT1=CORIOLIS(J)/
     1    (PLEVEL(K)-PLEVEL(K+2)) 
      FT2=(EDYGDT(I,J,K+1)+EDYGDT(I,J,K+2))*
     1    (PLEVEL(K+1)-PLEVEL(K+2))/                  
     2    (GLBAVT(K+2)-GLBAVT(K+1))
      FT3=(EDYGDT(I,J,K)+EDYGDT(I,J,K+1))*
     1    (PLEVEL(K)-PLEVEL(K+1))/                  
     2    (GLBAVT(K+1)-GLBAVT(K))
      TERM3=FT1*(FT2-FT3)
      EDYPTV(I,J,K)=EDYGRV(I,J,K)+TERM3
  725 CONTINUE
      RETURN
      END

CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C     compute spectral relative vort by using wind U & V
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      SUBROUTINE RELVOT(UWNREA,VWNREA,VORSPX)
      PARAMETER(NW=40)
      PARAMETER(NLEG=NW+1,NM=NW,NMP=NM+1,NLEGP=NLEG+1)
      PARAMETER(NLATG=102 ,KHALF=(NLATG+1)/2,NLONG=128 ,NLONGP=NLONG+1)
      PARAMETER(IMAXT=3*NLONG/2)
      PARAMETER(EA=6.371E+06)
      COMMON/GAUSS/RADG(NLATG),GWGT(NLATG)
      COMMON/POLY1/PLEG(NLEG,NMP,NLATG),DPLEG(NLEG,NMP,NLATG)
      COMMON/FFTAR1/TRIGS(IMAXT),IFAX(10)
      COMMON/FFTAR2/ARRAY(NLONGP,NLATG),WORK(NLONGP,NLATG)
      COMMON/FLDWAV/UWNWAV(2,NMP,NLATG),VWNWAV(2,NMP,NLATG)     
      DIMENSION VORSPX(2,NLEG,NMP),VORGRD(NLONG,NLATG)         
      DIMENSION UWNREA(NLONG,NLATG),VWNREA(NLONG,NLATG)             
      PI=ACOS(-1.)
C....WINDS MUST BE MULTIPLIED BY COS(LAT)
      DO 100 J=1,NLATG
      DO 100 I=1,NLONG
         UWNREA(I,J)=UWNREA(I,J)*COS(RADG(J))
         VWNREA(I,J)=VWNREA(I,J)*COS(RADG(J))
  100 CONTINUE
      CALL FTRANS(UWNREA,UWNWAV)
      CALL FTRANS(VWNREA,VWNWAV)
CCC---CALCULATE SPECTRAL VORTICITY FROM MODIFIED WINDS
      CALL VORWAV(UWNWAV,VWNWAV,VORSPX)
      RETURN
      END
 
      SUBROUTINE FTRANS(GRID,WAVE)
      PARAMETER(NW=40)
      PARAMETER(NLEG=NW+1,NM=NW,NMP=NM+1)
      PARAMETER(NLATG=102 ,NLONG=128 ,NLONGP=NLONG+1,IMAXT=3*NLONG/2)
      COMMON/FFTAR1/TRIGS(IMAXT),IFAX(10)
      COMMON/FFTAR2/ARRAY(NLONGP,NLATG),WORK(NLONGP,NLATG)
      DIMENSION GRID(NLONG,NLATG),WAVE(2,NMP,NLATG)
      DO 60 JG=1,NLATG
      DO 40 IG=1,NLONG
      ARRAY(IG,JG)=GRID(IG,JG)
   40 CONTINUE
      ARRAY(NLONGP,JG)=0.0
   60 CONTINUE
      ISIGN=-1
      INC=1
      JUMP=NLONGP
      LOT=NLATG
      CALL FFT991(ARRAY,WORK,TRIGS,IFAX,INC,JUMP,NLONG,LOT,ISIGN)
      DO 200 JG=1,NLATG
      DO 200 IM=1,NMP
      WAVE(1,IM,JG)=ARRAY(2*IM-1,JG)
      WAVE(2,IM,JG)=ARRAY(2*IM  ,JG)
  200 CONTINUE
      RETURN
      END
 
      SUBROUTINE VORWAV(U,V,VORT)
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C   PROGRAM TO TAKE THE WAVE REPRESENTATIONS OF THE MODIFIED     C  
C   WINDS AND CALCULATE THE SPECTRAL VORTICITY.                  C
C   IT IS ASSUMED THAT THE WINDS HAVE ALREADY BEEN MULTIPLIED    C
C   BY COS(LATITUDE)                                             C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      PARAMETER(NW=40)
      PARAMETER(NLEG=NW+1,NM=NW,NMP=NM+1,NLEGP=NLEG+1)
      PARAMETER(NLATG=102 ,NLONG=128 ,NLONGP=NLONG+1,IMAXT=3*NLONG/2)
      PARAMETER(EA=6.371E+06)
      COMMON/POLY1/PLEG(NLEG,NMP,NLATG),DPLEG(NLEG,NMP,NLATG)
      COMMON/GAUSS/RADG(NLATG),GWGT(NLATG)
      DIMENSION U(2,NMP,NLATG),V(2,NMP,NLATG)
      DIMENSION VORT(2,NLEG,NMP) 
      CALL FILLAR(VORT,0.0,2*NLEG*NMP)
      DO 300 J=1,NLATG
      COSR=COS(RADG(J))
      DO 200 IM=1,NMP
      XM=(IM-1)
      UREAL=U(1,IM,J)
      UIMAG=U(2,IM,J)
      VREAL=V(1,IM,J)
      VIMAG=V(2,IM,J)
      DO 180 N=1,NLEG
      VRE=(-XM*VIMAG*PLEG(N,IM,J)+UREAL*DPLEG(N,IM,J))
     1 /(COSR*COSR)
      VIM=(+XM*VREAL*PLEG(N,IM,J)+UIMAG*DPLEG(N,IM,J))
     1 /(COSR*COSR)
      VORT(1,N,IM)=VORT(1,N,IM)+GWGT(J)*VRE
      VORT(2,N,IM)=VORT(2,N,IM)+GWGT(J)*VIM
  180 CONTINUE
  200 CONTINUE
  300 CONTINUE
      DO 310 IM=1,NMP
      DO 310 N=1,NLEG
      DO 310 IC=1,2
      VORT(IC,N,IM)=VORT(IC,N,IM)/EA
  310 CONTINUE
      RETURN
      END

      SUBROUTINE FILLAR(ARRAYS,VALUE,N)
      DIMENSION ARRAYS(N)
      DO 20 I=1,N
      ARRAYS(I)=VALUE
   20 CONTINUE
      RETURN
      END

 

CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C  relative vort  over Gausssian grids
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      SUBROUTINE RLVORT(xin,yin,out)
      parameter(nlong=128,nlatg=102) 
      parameter(nlongw=nlong+2,nlatgw=nlatg+2) 
      COMMON/GAUSS/RADG(nlatg),GWGT(nlatg)
      dimension xin(nlong,nlatg),yin(nlong,nlatg)
      dimension out(nlong,nlatg)
      dimension workx(nlongw,nlatg)
      dimension worky(nlong,nlatgw)
      dimension coslat(nlatg)
      dimension wcos(nlatgw),wlat(nlatgw)
      r=6.37e+06
      pi=acos(-1.0)
      dlat=2*pi/nlong
         do 200 j=1,nlatg
         do 200 i=1,nlong
         workx(i+1,j)=yin(i,j)
         worky(i,j+1)=xin(i,j)
  200 continue
      do 300 j=1,nlatg 
         coslat(j)=cos(RADG(j))
         workx(1,j)=yin(nlong,j)
         workx(nlongw,j)=yin(1,j)
  300 continue
      do 400 i=1,nlong 
         worky(i,1)=0.
         worky(i,nlatgw)=0.
  400 continue
      do 450 j=1,nlatg
         wcos(j+1)=coslat(j)
         wlat(j+1)=RADG(j)   
  450 continue
         wcos(1)=0.
         wcos(nlatgw)=0.
         wlat(1)=pi/2.
         wlat(nlatgw)=-pi/2.
      do 520 j=1,nlatg
      do 520 i=1,nlong
         dx=(workx(i+2,j)-workx(i,j))/
     1      (r*coslat(j)*2*dlat)
         dy=(wcos(j+2)*worky(i,j+2)-wcos(j)*worky(i,j))
     1      /(r*wcos(j+1)*(wlat(j+2)-wlat(j)))
         out(i,j)=dx-dy
  520 continue
      return
      end

CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C  2-D average over Gausssian grids
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      subroutine AVERAGE(fin,out,ilat)
      parameter(nlong=128,nlatg=102)
      parameter(nlatgw=nlatg+2)
      COMMON/GAUSS/RADG(nlatg),GWGT(nlatg)
      dimension fin(nlong,nlatg)
      dimension xlat(nlatgw)
      pi=acos(-1.0)
      dlon=2*pi/128.
      fpi=4.*pi
      do 100 j=1,nlatg
         xlat(j+1)=RADG(j)
  100 continue
         xlat(1)=pi/2.
         xlat(nlatgw)=-pi/2.
      out=0.
      area=0.
      do 150 j=1,ilat
      rad=0.5*(xlat(j)+xlat(j+1))
      dlat=xlat(j)-xlat(j+1)
      do 150 i=1,nlong
         area=area+cos(rad)*dlon*dlat
  150 continue
      do 200 j=1,ilat
      rad=0.5*(xlat(j)+xlat(j+1))
      dlat=xlat(j)-xlat(j+1)
      do 200 i=1,nlong
         fild=0.5*(fin(i,j)+fin(i,j+1))
         out=out+fild*cos(rad)*dlon*dlat/area
  200 continue
      return
      end
 
