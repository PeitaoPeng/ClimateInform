      PARAMETER(NW=40)
      PARAMETER(MNWV2=2*(NW+1)*(NW+1))
      PARAMETER(NLEG=NW+1,NM=NW,NMP=NM+1)
      PARAMETER(NLATG=102,KHALF=(NLATG+1)/2,NLONG=128,NLONGP=NLONG+1)
      PARAMETER(IMAXT=3*NLONG/2)
      REAL*8 COLRAD,WGT,WGTCS,RCS2,PD,DPD
      COMMON/GAUSS/RADG(NLATG),GWGT(NLATG)
      COMMON/POLY1/PLEG(NLEG,NMP,NLATG),DPLEG(NLEG,NMP,NLATG)
      COMMON/FFTAR1/TRIGS(IMAXT),IFAX(10)
      COMMON/FFTAR2/ARRAY(NLONGP,NLATG),WORK(NLONGP,NLATG)
      DIMENSION PD(NLEG,NMP),DPD(NLEG,NMP)
      DIMENSION COLRAD(KHALF),WGT(KHALF),WGTCS(KHALF),RCS2(KHALF)
      DIMENSION UWND  (NLONG,NLATG),VWND  (NLONG,NLATG)
      DIMENSION UWNDR (NLONG,NLATG),VWNDR (NLONG,NLATG)
      DIMENSION UWNDD (NLONG,NLATG),VWNDD (NLONG,NLATG)
      DIMENSION VELPOT(NLONG,NLATG),STREAM(NLONG,NLATG)
      DIMENSION DIVS(2,NLEG,NMP),VORS(2,NLEG,NMP)
      DIMENSION VLPS(2,NLEG,NMP),STFS(2,NLEG,NMP)
      DIMENSION QZER(2,NLEG,NMP)
      DIMENSION UWND1 (NLONG,NLATG,7),VWND1 (NLONG,NLATG,7)
      DIMENSION GEOHT (NLONG,NLATG,7),TEMPT (NLONG,NLATG,7)
      OPEN(10,FILE='~/ppt/gcm1/ndmn88',FORM='UNFORMATTED',
     +RECORDTYPE='STREAM',CONVERT='BIG_ENDIAN')
      OPEN(80,FILE='~/ppt/gcm1/veltest',FORM='UNFORMATTED',
     +ACCESS='DIRECT',RECL=128*102)
      PI=ACOS(-1.0)
      CALL FILLSP(QZER,0.0,NLEG,NMP)
      CALL GLATS(KHALF,COLRAD,WGT,WGTCS,RCS2)
      DO 12 K=1,KHALF
      RADG(K)=PI/2.0 - COLRAD(K)
      RADG(NLATG-K+1)=-RADG(K)
      GWGT(K)=WGT(K)
      GWGT(NLATG-K+1)=+GWGT(K)
   12 CONTINUE
      DO 40 JG=1,NLATG
      CALL GENPNM(RADG(JG),PD,DPD)
      DO 30 M=1,NMP
      DO 30 N=1,NLEG
      PLEG(N,M,JG)=PD(N,M)
      DPLEG(N,M,JG)=DPD(N,M)
   30 CONTINUE
   40 CONTINUE
C***--- READ AVERAGE VELOCITIES FROM UNIT 8
      READ(10) GEOHT     
      READ(10) UWND1     
      READ(10) VWND1     
      READ(10) TEMPT     
      DO 50 J=1,NLATG
      DO 50 I=1,NLONG
         UWND(I,J)=UWND1(I,J,6)
         VWND(I,J)=VWND1(I,J,6)
   50 CONTINUE
CCC---CALCULATE SPECTRAL DIVERGENCE AND VORTICITY
      CALL GCMCRC(UWND,VWND,DIVS,VORS)
CCC---CALCULATE SPECTRAL VELOCITY POTENTIAL AND STREAMFUNCTION
      CALL DEL2IN(DIVS,VLPS)
      CALL DEL2IN(VORS,STFS)
C-----CALCULATE VELOCITY POTENTIAL AND STREAMFUNCTION ON GRID
      CALL EVLSCA(VLPS,VELPOT)
      CALL EVLSCA(STFS,STREAM)
CCC---CALCULATE ROTATIONAL WINDS (NO DIVERGENCE)
      CALL EVLU(QZER,STFS,UWNDR)
      CALL EVLV(QZER,STFS,VWNDR)
CCC---CALCULATE DIVERGENT WINDS (NO ROTATION)
      CALL EVLU(VLPS,QZER,UWNDD)
      CALL EVLV(VLPS,QZER,VWNDD)
      irec=1
      WRITE(80,rec=irec) STREAM
      irec=irec+1
      WRITE(80,rec=irec) UWNDR
      irec=irec+1
      WRITE(80,rec=irec) VWNDR
      irec=irec+1
      WRITE(80,rec=irec) VELPOT
      irec=irec+1
      WRITE(80,rec=irec) UWNDD
      irec=irec+1
      WRITE(80,rec=irec) VWNDD
      irec=irec+1
      WRITE(80,rec=irec) UWND 
      irec=irec+1
      WRITE(80,rec=irec) VWND 
      STOP
      END
 
      SUBROUTINE EVLSCA(ARRS,ARRP)
      PARAMETER(NW=40)
      PARAMETER(MNWV2=2*(NW+1)*(NW+1))
      PARAMETER(NLEG=NW+1,NM=NW,NMP=NM+1)
      PARAMETER(NLATG=102,NLONG=128,NLONGP=NLONG+1,IMAXT=3*NLONG/2)
      COMMON/POLY1/PLEG(NLEG,NMP,NLATG),DPLEG(NLEG,NMP,NLATG)
      COMMON/FFTAR1/TRIGS(IMAXT),IFAX(10)
      COMMON/FFTAR2/ARRAY(NLONGP,NLATG),WORK(NLONGP,NLATG)
      DIMENSION ARRS(2,NLEG,NMP),ARRP(NLONG,NLATG)
      IM1=2*NMP+1
      IM2=NLONGP
      DO 200 J=1,NLATG
      DO 120 IM=1,NMP
      DO 120 IC=1,2
      YSUM=0.0
      DO 100 N=1,NLEG
      YSUM=YSUM+ARRS(IC,N,IM)*PLEG(N,IM,J)
  100 CONTINUE
      ARRAY((IM-1)*2+IC,J)=YSUM
  120 CONTINUE
      DO 130 IM=IM1,IM2
      ARRAY(IM,J)=0.0
  130 CONTINUE
  200 CONTINUE
      INC=1
      ISIGN=+1
      JUMP=NLONGP
      LOT=NLATG
      CALL FFT991(ARRAY,WORK,TRIGS,IFAX,INC,JUMP,NLONG,LOT,ISIGN)
      DO 240 J=1,NLATG
      DO 240 I=1,NLONG
      ARRP(I,J)=ARRAY(I,J)
  240 CONTINUE
      RETURN
      END
 
      SUBROUTINE EVLU(VELP,STRM,ARRP)
      PARAMETER(NW=40)
      PARAMETER(NLEG=NW+1,NMP=NW+1)
      PARAMETER(NLATG=102,NLONG=128,NLONGP=NLONG+1,IMAXT=3*NLONG/2)
      PARAMETER(EA=6.371E+06)
      COMMON/GAUSS/RADG(NLATG),GWGT(NLATG)
      COMMON/POLY1/PLEG(NLEG,NMP,NLATG),DPLEG(NLEG,NMP,NLATG)
      COMMON/FFTAR1/TRIGS(IMAXT),IFAX(10)
      COMMON/FFTAR2/ARRAY(NLONGP,NLATG),WORK(NLONGP,NLATG)
      DIMENSION VELP(2,NLEG,NMP),STRM(2,NLEG,NMP)
      DIMENSION ARRP(NLONG,NLATG)
      IM1=2*NMP+1
      IM2=NLONGP
      DO 200 J=1,NLATG
      CONST=EA*COS(RADG(J))
      DO 120 IM=1,NMP
      XM=IM-1
      YSUMR=0.0
      YSUMI=0.0
      IF(IM .GT. 1) THEN
         NLOW=1
      ELSE
         NLOW=2
      END IF
      DO 100 N=NLOW,NLEG
      XN=XM+N-1
      YSUMR=YSUMR+(-XM*VELP(2,N,IM)* PLEG(N,IM,J)
     1             -   STRM(1,N,IM)*DPLEG(N,IM,J))
      YSUMI=YSUMI+( XM*VELP(1,N,IM)* PLEG(N,IM,J)
     1             -   STRM(2,N,IM)*DPLEG(N,IM,J))
  100 CONTINUE
      ARRAY((IM-1)*2+1,J)=YSUMR/CONST
      ARRAY((IM-1)*2+2,J)=YSUMI/CONST
  120 CONTINUE
      DO 130 IM=IM1,IM2
      ARRAY(IM,J)=0.0
  130 CONTINUE
  200 CONTINUE
      INC=1
      ISIGN=+1
      JUMP=NLONGP
      LOT=NLATG
      CALL FFT991(ARRAY,WORK,TRIGS,IFAX,INC,JUMP,NLONG,LOT,ISIGN)
      DO 240 J=1,NLATG
      DO 240 I=1,NLONG
      ARRP(I,J)=ARRAY(I,J)
  240 CONTINUE
CC    CALL WNDWR(FIELDG)
      RETURN
      END
 
      SUBROUTINE EVLV(VELP,STRM,ARRP)
      PARAMETER(NW=40)
      PARAMETER(NLEG=NW+1,NMP=NW+1)
      PARAMETER(NLATG=102,NLONG=128,NLONGP=NLONG+1,IMAXT=3*NLONG/2)
      PARAMETER(EA=6.371E+06)
      COMMON/GAUSS/RADG(NLATG),GWGT(NLATG)
      COMMON/POLY1/PLEG(NLEG,NMP,NLATG),DPLEG(NLEG,NMP,NLATG)
      COMMON/FFTAR1/TRIGS(IMAXT),IFAX(10)
      COMMON/FFTAR2/ARRAY(NLONGP,NLATG),WORK(NLONGP,NLATG)
      DIMENSION VELP(2,NLEG,NMP),STRM(2,NLEG,NMP)
      DIMENSION ARRP(NLONG,NLATG)
      IM1=2*NMP+1
      IM2=NLONGP
      DO 200 J=1,NLATG
      CONST=EA*COS(RADG(J))
      DO 120 IM=1,NMP
      XM=IM-1
      YSUMR=0.0
      YSUMI=0.0
      IF(IM .GT. 1) THEN
         NLOW=1
      ELSE
         NLOW=2
      END IF
      DO 100 N=NLOW,NLEG
      XN=XM+N-1
      YSUMR=YSUMR+(-XM*STRM(2,N,IM)* PLEG(N,IM,J)
     1             +   VELP(1,N,IM)*DPLEG(N,IM,J))
      YSUMI=YSUMI+( XM*STRM(1,N,IM)* PLEG(N,IM,J)
     1             +   VELP(2,N,IM)*DPLEG(N,IM,J))
  100 CONTINUE
      ARRAY((IM-1)*2+1,J)=YSUMR/CONST
      ARRAY((IM-1)*2+2,J)=YSUMI/CONST
  120 CONTINUE
      DO 130 IM=IM1,IM2
      ARRAY(IM,J)=0.0
  130 CONTINUE
  200 CONTINUE
      INC=1
      ISIGN=+1
      JUMP=NLONGP
      LOT=NLATG
      CALL FFT991(ARRAY,WORK,TRIGS,IFAX,INC,JUMP,NLONG,LOT,ISIGN)
      DO 240 J=1,NLATG
      DO 240 I=1,NLONG
      ARRP(I,J)=ARRAY(I,J)
  240 CONTINUE
      RETURN
      END
 
      SUBROUTINE FTRANS(GRID,WAVE)
      PARAMETER(NW=40)
      PARAMETER(MNWV2=2*(NW+1)*(NW+1))
      PARAMETER(NLEG=NW+1,NM=NW,NMP=NM+1)
      PARAMETER(NLATG=102,NLONG=128,NLONGP=NLONG+1,IMAXT=3*NLONG/2)
      COMMON/FFTAR1/TRIGS(IMAXT),IFAX(10)
      COMMON/FFTAR2/ARRAY(NLONGP,NLATG),WORK(NLONGP,NLATG)
      DIMENSION GRID(NLONG,NLATG),WAVE(2,NMP,NLATG)
      DO 60 JG=1,NLATG
      DO 40 IG=1,NLONG
      ARRAY(IG,JG)=GRID(IG,JG)
   40 CONTINUE
      ARRAY(NLONGP,JG)=0.0
   60 CONTINUE
      ISIGN=-1
      INC=1
      JUMP=NLONGP
      LOT=NLATG
      CALL FFT991(ARRAY,WORK,TRIGS,IFAX,INC,JUMP,NLONG,LOT,ISIGN)
      DO 200 JG=1,NLATG
      DO 200 IM=1,NMP
      WAVE(1,IM,JG)=ARRAY(2*IM-1,JG)
      WAVE(2,IM,JG)=ARRAY(2*IM  ,JG)
  200 CONTINUE
      RETURN
      END
 
      SUBROUTINE DEL2IN(ARRAYI,ARRAYO)
      PARAMETER(NW=40)
      PARAMETER(NLEG=NW+1,NM=NW,NMP=NM+1)
      PARAMETER(EA=6.371E+06)
      DIMENSION ARRAYI(2,NLEG,NMP),ARRAYO(2,NLEG,NMP)
      DO 100 IM=1,NMP
      XM=IM-1
      DO 80 N=1,NLEG
      XN=N+XM-1.0
      IF(XN .LT. .001) THEN
         ARRAYO(1,N,IM)=0.0
         ARRAYO(2,N,IM)=0.0
      ELSE
         DELSQI=-EA*EA/(XN*(XN+1.0))
         ARRAYO(1,N,IM)=ARRAYI(1,N,IM)*DELSQI
         ARRAYO(2,N,IM)=ARRAYI(2,N,IM)*DELSQI
      END IF
   80 CONTINUE
  100 CONTINUE
      RETURN
      END
 
      SUBROUTINE GCMCRC(UVEC,VVEC,DIV,VOR)
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C   PROGRAM TO TAKE THE (U,V) COMPONENTS OF THE WIND ON THE G. GRID    C
C   AND COMPUTE SPECTRAL DIVERGENCE (DIV) AND VORTICITY (VOR)          C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      PARAMETER(NW=40)
      PARAMETER(MNWV2=2*(NW+1)*(NW+1))
      PARAMETER(NLEG=NW+1,NM=NW,NMP=NM+1)
      PARAMETER(NLATG=102,NLONG=128,NLONGP=NLONG+1,IMAXT=3*NLONG/2)
      PARAMETER(EA=6.371E+06)
      COMMON/POLY1/PLEG(NLEG,NMP,NLATG),DPLEG(NLEG,NMP,NLATG)
      COMMON/GAUSS/RADG(NLATG),GWGT(NLATG)
      DIMENSION UVEC(NLONG,NLATG),VVEC(NLONG,NLATG)
      DIMENSION DIV(2,NLEG,NMP),VOR(2,NLEG,NMP)
      DIMENSION USPEC(2,NMP,NLATG),VSPEC(2,NMP,NLATG)
      DIMENSION UM(NLONGP),VM(NLONGP)
      CALL FTRANS(UVEC,USPEC)
      CALL FTRANS(VVEC,VSPEC)
      IM1=2*NMP+1
      IM2=NLONGP
      CALL FILLSP(DIV,0.0,NLEG,NMP)
      CALL FILLSP(VOR,0.0,NLEG,NMP)
      DO 300 J=1,NLATG
      COSR=COS(RADG(J))
      DO 120 IM=1,NMP
      DO 120 IC=1,2
      UM((IM-1)*2+IC)=USPEC(IC,IM,J)*COSR
      VM((IM-1)*2+IC)=VSPEC(IC,IM,J)*COSR
  120 CONTINUE
      DO 130 IM=IM1,IM2
      UM(IM)=0.0
      VM(IM)=0.0
  130 CONTINUE
      DO 200 IM=1,NMP
      XM=(IM-1)
      UREAL=UM((IM-1)*2+1)
      UIMAG=UM((IM-1)*2+2)
      VREAL=VM((IM-1)*2+1)
      VIMAG=VM((IM-1)*2+2)
      DO 180 N=1,NLEG
      DRE=(-XM*UIMAG*PLEG(N,IM,J)-VREAL*DPLEG(N,IM,J))/(COSR*COSR)
      DIM=(+XM*UREAL*PLEG(N,IM,J)-VIMAG*DPLEG(N,IM,J))/(COSR*COSR)
      DIV(1,N,IM)=DIV(1,N,IM)+DRE*GWGT(J)
      DIV(2,N,IM)=DIV(2,N,IM)+DIM*GWGT(J)
  180 CONTINUE
      DO 190 N=1,NLEG
      VRE=(-XM*VIMAG*PLEG(N,IM,J)+UREAL*DPLEG(N,IM,J))/(COSR*COSR)
      VIM=(+XM*VREAL*PLEG(N,IM,J)+UIMAG*DPLEG(N,IM,J))/(COSR*COSR)
      VOR(1,N,IM)=VOR(1,N,IM)+VRE*GWGT(J)
      VOR(2,N,IM)=VOR(2,N,IM)+VIM*GWGT(J)
  190 CONTINUE
  200 CONTINUE
  300 CONTINUE
      DO 310 IM=1,NMP
      DO 310 N=1,NLEG
      DO 310 IC=1,2
      DIV(IC,N,IM)=DIV(IC,N,IM)/EA
  310 CONTINUE
      DO 320 IM=1,NMP
      DO 320 N=1,NLEG
      DO 320 IC=1,2
      VOR(IC,N,IM)=VOR(IC,N,IM)/EA
  320 CONTINUE
      RETURN
      END
 
      SUBROUTINE FILLSP(ARRAYS,VALUE,NLEG,NMP)
      DIMENSION ARRAYS(2,NLEG,NMP)
      DO 20 IM=1,NMP
      DO 20 N=1,NLEG
      ARRAYS(1,N,IM)=VALUE
      ARRAYS(2,N,IM)=VALUE
   20 CONTINUE
      RETURN
      END
 
      SUBROUTINE GRDCMF(UVEC,VVEC,CMFGRD)
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C   PROGRAM TO TAKE COMPONENTS OF ANY FLUX (UVEC,VVEC) ON THE GRID,    C
C   AND COMPUTE CONVERGENCE WITH FINITE-DIFFERENCES                    C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      PARAMETER(NLATG=102,NLONG=128,NLATGM=NLATG-1)
      PARAMETER(EA=6.371E+06)
      COMMON/GAUSS/RADG(NLATG),GWGT(NLATG)
      DIMENSION UVEC(NLONG,NLATG),VVEC(NLONG,NLATG),CMFGRD(NLONG,NLATG)
      DIMENSION XLAT(NLATG)
      DIMENSION IP1(NLONG),IM1(NLONG)
      PI=ACOS(-1.0)
      XNLONG=NLONG
      DO 10 J=1,NLATG
      XLAT(J)=SIN(RADG(J))
   10 CONTINUE
      DLON=2.0*PI/XNLONG
      DO 30 I=1,NLONG
      IP1(I)=I+1
      IM1(I)=I-1
      IF(I .EQ. NLONG) IP1(I)=1
      IF(I .EQ.     1) IM1(I)=NLONG
   30 CONTINUE
      DO 120 J=2,NLATGM
      COSP=COS(RADG(J+1))
      COSR=COS(RADG(J  ))
      COSM=COS(RADG(J-1))
      DLAT=(XLAT(J+1) - XLAT(J-1))
      DO 100 I=1,NLONG
      XDIFF=(UVEC(IP1(I),J) - UVEC(IM1(I),J))/(COSR*DLON)
      YDIFF=(VVEC(I,J+1)*COSP - VVEC(I,J-1)*COSM)/DLAT
      CMFGRD(I,J)=-(XDIFF+YDIFF)/EA
  100 CONTINUE
  120 CONTINUE
      RETURN
      END
      SUBROUTINE GENPNM(DRAD,PLEGND,DPDU)
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C DRAD    (INPUT) = LATITUDE IN RADIANS                                C
C PLEGND (OUTPUT) = VALUES OF LEGENDRE POLYNOMIALS                     C
C                    FOR ALL INDICES SPECIFIED BY NM AND NN            C
C DPDU   (OUTPUT) = VALUES OF  (1.0-U*U)*D(LEG. POLY.)/D(U),           C
C                    WHERE U = SIN(LATITUDE),                          C
C                    FOR ALL INDICES SPECIFIED BY NM AND NN            C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      IMPLICIT REAL*8 (A-H,O-Z)
      REAL*4 DRAD
      PARAMETER(NM=40,NN=NM+1,NMP=NM+1,NUP=NM)
      DIMENSION PLEGND(NN,NMP),DPDU(NN,NMP),EPS(NN,NMP)
      DIMENSION XXN(NN,NMP),XXM(NMP)
      XCOS=COS(DRAD)
      XSIN=SIN(DRAD)
      DO 6 IM=1,NMP
      XXM(IM)=IM-1
      DO 6 N=1,NN
      XXN(N,IM)=XXM(IM)+N-1
    6 CONTINUE
      EPS(1,1)=0.0
      DO 10 N=2,NN
      XN=XXN(N,1)
      EPS(N,1)=DSQRT(XN*XN/(4.0D0*XN*XN-1.0D0))
   10 CONTINUE
      DO 20 IM=2,NMP
      XM=XXM(IM)
      DO 18 N=1,NN
      XN=XXN(N,IM)
      EPS(N,IM)=DSQRT((XN*XN-XM*XM)/(4.0D0*XN*XN-1.0D0))
   18 CONTINUE
   20 CONTINUE
C----------------------------------------------------------------------
C     POLYNOMIALS AND DERIVATIVES FOR IM=1 (ZERO WAVENUMBER)
C----------------------------------------------------------------------
      U=XSIN
      SQ2=DSQRT(2.0D0)
      SQ3=DSQRT(3.0D0)
      PLEGND(1,1)=1.0D0/SQ2
      PLEGND(2,1)=U*SQ3/SQ2
      DPDU  (1,1)=0.0D0
      DPDU  (2,1)=SQ3/SQ2*(1.0D0-U*U)
      DO 40 N=2,NUP
      PLEGND(N+1,1)=(U*PLEGND(N,1)-EPS(N,1)*PLEGND(N-1,1))/EPS(N+1,1)
   40 CONTINUE
      DO 60 N=3,NN
      XN=XXN(N,1)
      DPDU(N,1)=(2.0D0*XN+1.0D0)*EPS(N,1)*PLEGND(N-1,1) -
     1           XN*U*PLEGND(N,1)
   60 CONTINUE
C----------------------------------------------------------------------
C     POLYNOMIALS AND DERIVATIVES FOR IM=.GT. 1 (NONZERO WAVENUMBERS)
C----------------------------------------------------------------------
      PREFAC=SQ3/2.0D0*XCOS
      DO 200 IM=2,NMP
      XM=XXM(IM)
      PLEGND(1,IM)=PREFAC
      DPDU  (1,IM)=-XM*U*PLEGND(1,IM)
      PLEGND(2,IM)=U*PLEGND(1,IM)/EPS(2,IM)
      DPDU  (2,IM)=-(XM+1.0D0)*U*PLEGND(2,IM) +
     1(2.0D0*XM+3.0D0)*EPS(2,IM)*PLEGND(1,IM)
      DO 140 N=2,NUP
      PLEGND(N+1,IM)=(U*PLEGND(N,IM)-EPS(N,IM)*PLEGND(N-1,IM))
     1 /EPS(N+1,IM)
  140 CONTINUE
      DO 160 N=3,NN
      XN=XXN(N,IM)
      COEF1=(2.0D0*XN+1.0D0)
      DPDU(N,IM)=COEF1*EPS(N,IM)*PLEGND(N-1,IM)-XN*U*PLEGND(N,IM)
  160 CONTINUE
      PREFAC=PREFAC*DSQRT((2.0D0*XM+3.0D0)/(2.0D0*(XM+1.0D0)))*XCOS
      IF(PREFAC .LT. 1.0D-40) PREFAC=0.0D0
  200 CONTINUE
      RETURN
      END
