      PARAMETER(NW=15)
      PARAMETER(NLEG=NW+1,NM=NW,NMP=NM+1,NLEGP=NLEG+1)
      PARAMETER(NLATG=40 ,KHALF=(NLATG+1)/2,NLONG=48 ,NLONGP=NLONG+1)
      PARAMETER(IMAXT=3*NLONG/2)
      PARAMETER(EA=6.371E+06)
      REAL*8 COLRAD,WGT,WGTCS,RCS2,PLEG,DPLEG
      COMMON/GAUSS/RADG(NLATG),GWGT(NLATG)
      COMMON/POLY1/PLEG(NLEG,NMP,NLATG),DPLEG(NLEG,NMP,NLATG)
      COMMON/FFTAR1/TRIGS(IMAXT),IFAX(10)
      COMMON/FFTAR2/ARRAY(NLONGP,NLATG),WORK(NLONGP,NLATG)
      COMMON/FLDREA/UWNREA(NLONG,NLATG),VWNREA(NLONG,NLATG)             
      COMMON/FLDWAV/UWNWAV(2,NMP,NLATG),VWNWAV(2,NMP,NLATG)     
      COMMON/SPXROT/VORSPX(2,NLEG,NMP),VORGRD(NLONG,NLATG)         
      DIMENSION COLRAD(KHALF),WGT(KHALF),WGTCS(KHALF),RCS2(KHALF)
      OPEN(8,FILE='~/ppt/gcm1/uv15.dat',FORM='UNFORMATTED',
     +ACCESS='DIRECT',RECL=NLONG*NLATG)
      OPEN(80,FILE='~/ppt/gcm1/vort87w',FORM='UNFORMATTED',
     +ACCESS='DIRECT',RECL=NLONG*NLATG)
      PI=ACOS(-1.)
      CALL GLATS(KHALF,COLRAD,WGT,WGTCS,RCS2)
      DO 12 K=1,KHALF
      RADG(K)=PI/2.0 - COLRAD(K)
      RADG(NLATG-K+1)=-RADG(K)
      GWGT(K)=WGT(K)
      GWGT(NLATG-K+1)=+GWGT(K)
   12 CONTINUE
      DO 40 JG=1,NLATG
      CALL GENPNM(RADG(JG),PLEG(1,1,JG),DPLEG(1,1,JG))
   40 CONTINUE
C....WINDS MUST BE MULTIPLIED BY COS(LAT)
        READ(8,rec=4) UWNREA    
      WRITE(80,rec=1) UWNREA    
        READ(8,rec=11) VWNREA    
      WRITE(80,rec=2) VWNREA    
      DO 100 J=1,NLATG
      DO 100 I=1,NLONG
         UWNREA(I,J)=UWNREA(I,J)*COS(RADG(NLATG+1-J))
         VWNREA(I,J)=VWNREA(I,J)*COS(RADG(NLATG+1-J))
  100 CONTINUE
      CALL FTRANS(UWNREA,UWNWAV)
      CALL FTRANS(VWNREA,VWNWAV)
CCC---CALCULATE SPECTRAL VORTICITY FROM MODIFIED WINDS
      CALL VORWAV(UWNWAV,VWNWAV,VORSPX)
C-----TRANSFORM VORSPX TO GAUSSIAN GRIDS
      CALL EVLSCA(VORSPX,VORGRD)
      WRITE(80,rec=3) VORGRD    
      STOP
      END
 
      SUBROUTINE FTRANS(GRID,WAVE)
      PARAMETER(NW=15)
      PARAMETER(NLEG=NW+1,NM=NW,NMP=NM+1)
      PARAMETER(NLATG=40 ,NLONG=48 ,NLONGP=NLONG+1,IMAXT=3*NLONG/2)
      COMMON/FFTAR1/TRIGS(IMAXT),IFAX(10)
      COMMON/FFTAR2/ARRAY(NLONGP,NLATG),WORK(NLONGP,NLATG)
      DIMENSION GRID(NLONG,NLATG),WAVE(2,NMP,NLATG)
      DO 60 JG=1,NLATG
      DO 40 IG=1,NLONG
      ARRAY(IG,JG)=GRID(IG,JG)
   40 CONTINUE
      ARRAY(NLONGP,JG)=0.0
   60 CONTINUE
      ISIGN=-1
      INC=1
      JUMP=NLONGP
      LOT=NLATG
      CALL FFT991(ARRAY,WORK,TRIGS,IFAX,INC,JUMP,NLONG,LOT,ISIGN)
      DO 200 JG=1,NLATG
      DO 200 IM=1,NMP
      WAVE(1,IM,JG)=ARRAY(2*IM-1,JG)
      WAVE(2,IM,JG)=ARRAY(2*IM  ,JG)
  200 CONTINUE
      RETURN
      END
 
      SUBROUTINE VORWAV(U,V,VORT)
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C   PROGRAM TO TAKE THE WAVE REPRESENTATIONS OF THE MODIFIED     C  
C   WINDS AND CALCULATE THE SPECTRAL VORTICITY.                  C
C   IT IS ASSUMED THAT THE WINDS HAVE ALREADY BEEN MULTIPLIED    C
C   BY COS(LATITUDE)                                             C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      PARAMETER(NW=15)
      PARAMETER(NLEG=NW+1,NM=NW,NMP=NM+1,NLEGP=NLEG+1)
      PARAMETER(NLATG=40 ,NLONG=48 ,NLONGP=NLONG+1,IMAXT=3*NLONG/2)
      PARAMETER(EA=6.371E+06)
      REAL*8 COLRAD,WGT,WGTCS,RCS2,PLEG,DPLEG
      COMMON/POLY1/PLEG(NLEG,NMP,NLATG),DPLEG(NLEG,NMP,NLATG)
      COMMON/GAUSS/RADG(NLATG),GWGT(NLATG)
      DIMENSION U(2,NMP,NLATG),V(2,NMP,NLATG)
      DIMENSION VORT(2,NLEG,NMP) 
      CALL FILLAR(VORT,0.0,2*NLEG*NMP)
      DO 300 J=1,NLATG
      COSR=COS(RADG(J))
      DO 200 IM=1,NMP
      XM=(IM-1)
      UREAL=U(1,IM,J)
      UIMAG=U(2,IM,J)
      VREAL=V(1,IM,J)
      VIMAG=V(2,IM,J)
      DO 180 N=1,NLEG
      VRE=(-XM*VIMAG*PLEG(N,IM,J)+UREAL*DPLEG(N,IM,J))
     1 /(COSR*COSR)
      VIM=(+XM*VREAL*PLEG(N,IM,J)+UIMAG*DPLEG(N,IM,J))
     1 /(COSR*COSR)
      VORT(1,N,IM)=VORT(1,N,IM)+GWGT(J)*VRE
      VORT(2,N,IM)=VORT(2,N,IM)+GWGT(J)*VIM
  180 CONTINUE
  200 CONTINUE
  300 CONTINUE
      DO 310 IM=1,NMP
      DO 310 N=1,NLEG
      DO 310 IC=1,2
      VORT(IC,N,IM)=VORT(IC,N,IM)/EA
  310 CONTINUE
      RETURN
      END

      SUBROUTINE FILLAR(ARRAYS,VALUE,N)
      DIMENSION ARRAYS(N)
      DO 20 I=1,N
      ARRAYS(I)=VALUE
   20 CONTINUE
      RETURN
      END

CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C  transform from spctral to Gaussian grid                        C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      SUBROUTINE EVLSCA(ARRS,ARRP)
      PARAMETER(NW=15)
      PARAMETER(MNWV2=2*(NW+1)*(NW+1))
      PARAMETER(NLEG=NW+1,NM=NW,NMP=NM+1)
      PARAMETER(NLATG=40 ,NLONG=48 ,NLONGP=NLONG+1,IMAXT=3*NLONG/2)
      REAL*8 COLRAD,WGT,WGTCS,RCS2,PLEG,DPLEG
      COMMON/POLY1/PLEG(NLEG,NMP,NLATG),DPLEG(NLEG,NMP,NLATG)
      COMMON/FFTAR1/TRIGS(IMAXT),IFAX(10)
      COMMON/FFTAR2/ARRAY(NLONGP,NLATG),WORK(NLONGP,NLATG)
      DIMENSION ARRS(2,NLEG,NMP),ARRP(NLONG,NLATG)
      IM1=2*NMP+1
      IM2=NLONGP
      DO 200 J=1,NLATG
      DO 120 IM=1,NMP
      DO 120 IC=1,2
      YSUM=0.0
      DO 100 N=1,NLEG
      YSUM=YSUM+ARRS(IC,N,IM)*PLEG(N,IM,J)
  100 CONTINUE
      ARRAY((IM-1)*2+IC,J)=YSUM
  120 CONTINUE
      DO 130 IM=IM1,IM2
      ARRAY(IM,J)=0.0
  130 CONTINUE
  200 CONTINUE
      INC=1
      ISIGN=+1
      JUMP=NLONGP
      LOT=NLATG
      CALL FFT991(ARRAY,WORK,TRIGS,IFAX,INC,JUMP,NLONG,LOT,ISIGN)
      DO 240 J=1,NLATG
      DO 240 I=1,NLONG
      ARRP(I,J)=ARRAY(I,J)
  240 CONTINUE
      RETURN
      END

      SUBROUTINE GENPNM(DRAD,PLEGND,DPDU)
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C DRAD    (INPUT) = LATITUDE IN RADIANS                                C
C PLEGND (OUTPUT) = VALUES OF LEGENDRE POLYNOMIALS                     C
C                    FOR ALL INDICES SPECIFIED BY NM AND NN            C
C DPDU   (OUTPUT) = VALUES OF  (1.0-U*U)*D(LEG. POLY.)/D(U),           C
C                    WHERE U = SIN(LATITUDE),                          C
C                    FOR ALL INDICES SPECIFIED BY NM AND NN            C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      IMPLICIT REAL*8 (A-H,O-Z)
      REAL*4 DRAD
      PARAMETER(NM=15,NN=NM+1,NMP=NM+1,NUP=NM)
      DIMENSION PLEGND(NN,NMP),DPDU(NN,NMP),EPS(NN,NMP)
      DIMENSION XXN(NN,NMP),XXM(NMP)
      XCOS=COS(DRAD)
      XSIN=SIN(DRAD)
      DO 6 IM=1,NMP
      XXM(IM)=IM-1
      DO 6 N=1,NN
      XXN(N,IM)=XXM(IM)+N-1
    6 CONTINUE
      EPS(1,1)=0.0
      DO 10 N=2,NN
      XN=XXN(N,1)
      EPS(N,1)=DSQRT(XN*XN/(4.0D0*XN*XN-1.0D0))
   10 CONTINUE
      DO 20 IM=2,NMP
      XM=XXM(IM)
      DO 18 N=1,NN
      XN=XXN(N,IM)
      EPS(N,IM)=DSQRT((XN*XN-XM*XM)/(4.0D0*XN*XN-1.0D0))
   18 CONTINUE
   20 CONTINUE
C----------------------------------------------------------------------
C     POLYNOMIALS AND DERIVATIVES FOR IM=1 (ZERO WAVENUMBER)
C----------------------------------------------------------------------
      U=XSIN
      SQ2=DSQRT(2.0D0)
      SQ3=DSQRT(3.0D0)
      PLEGND(1,1)=1.0D0/SQ2
      PLEGND(2,1)=U*SQ3/SQ2
      DPDU  (1,1)=0.0D0
      DPDU  (2,1)=SQ3/SQ2*(1.0D0-U*U)
      DO 40 N=2,NUP
      PLEGND(N+1,1)=(U*PLEGND(N,1)-EPS(N,1)*PLEGND(N-1,1))/EPS(N+1,1)
   40 CONTINUE
      DO 60 N=3,NN
      XN=XXN(N,1)
      DPDU(N,1)=(2.0D0*XN+1.0D0)*EPS(N,1)*PLEGND(N-1,1) -
     1           XN*U*PLEGND(N,1)
   60 CONTINUE
C----------------------------------------------------------------------
C     POLYNOMIALS AND DERIVATIVES FOR IM=.GT. 1 (NONZERO WAVENUMBERS)
C----------------------------------------------------------------------
      PREFAC=SQ3/2.0D0*XCOS
      DO 200 IM=2,NMP
      XM=XXM(IM)
      PLEGND(1,IM)=PREFAC
      DPDU  (1,IM)=-XM*U*PLEGND(1,IM)
      PLEGND(2,IM)=U*PLEGND(1,IM)/EPS(2,IM)
      DPDU  (2,IM)=-(XM+1.0D0)*U*PLEGND(2,IM) +
     1(2.0D0*XM+3.0D0)*EPS(2,IM)*PLEGND(1,IM)
      DO 140 N=2,NUP
      PLEGND(N+1,IM)=(U*PLEGND(N,IM)-EPS(N,IM)*PLEGND(N-1,IM))
     1 /EPS(N+1,IM)
  140 CONTINUE
      DO 160 N=3,NN
      XN=XXN(N,IM)
      COEF1=(2.0D0*XN+1.0D0)
      DPDU(N,IM)=COEF1*EPS(N,IM)*PLEGND(N-1,IM)-XN*U*PLEGND(N,IM)
  160 CONTINUE
      PREFAC=PREFAC*DSQRT((2.0D0*XM+3.0D0)/(2.0D0*(XM+1.0D0)))*XCOS
      IF(PREFAC .LT. 1.0D-40) PREFAC=0.0D0
  200 CONTINUE
      RETURN
      END
