      SUBROUTINE GRADSP(F,DFDX,DFDY)
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C F       (INPUT) = SPECTRAL FIELD                                     C
C DFDX   (OUTPUT) = (1.0/EA)*D(F)/D(LAMDA) (SPECTRAL)                  C
C DFDY   (OUTPUT) = (1.0/EA)*COS(PHI)*D(F)/D(PHI)          ,           C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      PARAMETER(NW=40)
      PARAMETER(NMP=NW+1)
      PARAMETER(NLEG=NW+1)
      PARAMETER(EA=6.371E+06)
      DIMENSION F(2,NLEG,NMP),DFDX(2,NLEG,NMP),DFDY(2,NLEG,NMP)
      DIMENSION EPS(NLEG,NMP)
      DIMENSION XXN(NLEG,NMP),XXM(NMP)
      DO 6 IM=1,NMP
      XXM(IM)=IM-1
      DO 6 N=1,NLEG
      XXN(N,IM)=XXM(IM)+N-1
    6 CONTINUE
      EPS(1,1)=0.0
      DO 10 N=2,NLEG
      XN=XXN(N,1)
      EPS(N,1)= SQRT(XN*XN/(4.0  *XN*XN-1.0  ))
   10 CONTINUE
      DO 20 IM=2,NMP
      XM=XXM(IM)
      DO 18 N=1,NLEG
      XN=XXN(N,IM)
      EPS(N,IM)= SQRT((XN*XN-XM*XM)/(4.0  *XN*XN-1.0  ))
   18 CONTINUE
   20 CONTINUE
      DO 100 IM=1,NMP
      DO 40 N=1,NLEG
      XM=XXM(IM)
      DFDX(1,N,IM)=-XM*F(2,N,IM)
      DFDX(2,N,IM)=+XM*F(1,N,IM)
   40 CONTINUE
      DO 60 N=1,NLEG
      XN=XXN(N,IM)
      IF(N .GT. 1 .AND. N .LT. NLEG) THEN
        DFDY(1,N,IM)=(XN+2.0)*EPS(N+1,IM)*F(1,N+1,IM)
     1             - (XN-1.0)*EPS(N  ,IM)*F(1,N-1,IM)
        DFDY(2,N,IM)=(XN+2.0)*EPS(N+1,IM)*F(2,N+1,IM)
     1             - (XN-1.0)*EPS(N  ,IM)*F(2,N-1,IM)
      ELSE IF(N .EQ. 1) THEN
        DFDY(1,N,IM)=(XN+2.0)*EPS(N+1,IM)*F(1,N+1,IM)
        DFDY(2,N,IM)=(XN+2.0)*EPS(N+1,IM)*F(2,N+1,IM)
      ELSE
        DFDY(1,N,IM)=
     1             - (XN-1.0)*EPS(N  ,IM)*F(1,N-1,IM)
        DFDY(2,N,IM)=
     1             - (XN-1.0)*EPS(N  ,IM)*F(2,N-1,IM)
      END IF
      DFDX(1,N,IM)=DFDX(1,N,IM)/EA
      DFDX(2,N,IM)=DFDX(2,N,IM)/EA
      DFDY(1,N,IM)=DFDY(1,N,IM)/EA
      DFDY(2,N,IM)=DFDY(2,N,IM)/EA
   60 CONTINUE
  100 CONTINUE
      RETURN
      END
