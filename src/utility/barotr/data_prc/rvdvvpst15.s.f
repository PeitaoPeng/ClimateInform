CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C  from U & V wind component compute
C       vorticity
C       divergence
C       velocity potential
C       stream unction
C       rotational wind
C       divergent wind
C  data must be from north pole to south pole
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      SUBROUTINE RVDVVPST15(UWND,VWND,UWNDR,VWNDR,UWNDD,VWNDD,
     +                      STREAM,DIV)
c     SUBROUTINE RVDVVPST15(UWND,VWND,UWNDR,VWNDR,UWNDD,VWNDD,
c    +                      VOR,DIV)
c     SUBROUTINE RVDVVPST15(UWND,VWND,UWNDR,VWNDR,UWNDD,VWNDD,
c    +                      STREAM,VELPOT)
      PARAMETER(NW=15)
      PARAMETER(NLEG=NW+1,NM=NW,NMP=NM+1)
      PARAMETER(NLATG=40,KHALF=(NLATG+1)/2,NLONG=64,NLONGP=NLONG+1)
      PARAMETER(IMAXT=3*NLONG/2)
      COMMON/GAUSS/RADG(NLATG),GWGT(NLATG)
      COMMON/POLY1/PLEG(NLEG,NMP,NLATG),DPLEG(NLEG,NMP,NLATG)
      COMMON/FFTAR1/TRIGS(IMAXT),IFAX(10)
      COMMON/FFTAR2/ARRAY(NLONGP,NLATG),WORK(NLONGP,NLATG)
      REAL*8    COLRAD(KHALF),WGT(KHALF),WGTCS(KHALF),RCS2(KHALF)
      DIMENSION UWND  (NLONG,NLATG),VWND  (NLONG,NLATG)
      DIMENSION UWNDR (NLONG,NLATG),VWNDR (NLONG,NLATG)
      DIMENSION UWNDD (NLONG,NLATG),VWNDD (NLONG,NLATG)
      DIMENSION VELPOT(NLONG,NLATG),STREAM(NLONG,NLATG)
      DIMENSION DIV (NLONG,NLATG),VOR (NLONG,NLATG)
      DIMENSION DIVS(2,NLEG,NMP),VORS(2,NLEG,NMP)
      DIMENSION VLPS(2,NLEG,NMP),STFS(2,NLEG,NMP)
      DIMENSION QZER(2,NLEG,NMP)
      PI=ACOS(-1.0)
      CALL GLATS(KHALF,COLRAD,WGT,WGTCS,RCS2)
      DO 10 K=1,KHALF
      RADG(K)=PI/2.0 - REAL(COLRAD(K))
      RADG(NLATG-K+1)=-RADG(K)
      GWGT(K)=REAL(WGT(K))
      GWGT(NLATG-K+1)=+GWGT(K)
   10 CONTINUE
      DO 20 JG=1,NLATG
      CALL GENPNME15(RADG(JG),PLEG(1,1,JG),DPLEG(1,1,JG))
   20 CONTINUE
      CALL FILLSP(QZER,0.0,NLEG,NMP)
CCC---CALCULATE SPECTRAL DIVERGENCE AND VORTICITY
c     write(6,*) 'before GCMCRC'
      CALL GCMCRC(UWND,VWND,DIVS,VORS)
CCC---CALCULATE SPECTRAL VELOCITY POTENTIAL AND STREAMFUNCTION
c     write(6,*) 'before DEL2IN'
      CALL DEL2IN(DIVS,VLPS)
      CALL DEL2IN(VORS,STFS)
C-----CALCULATE VELOCITY POTENTIAL AND STREAMFUNCTION ON GRID
c     write(6,*) 'before EVLSCA'
      CALL EVLSCA15(VLPS,VELPOT)
      CALL EVLSCA15(STFS,STREAM)
CCC---CALCULATE ROTATIONAL WINDS (NO DIVERGENCE)
c     write(6,*) 'before EVLUV'
      CALL EVLU(QZER,STFS,UWNDR)
      CALL EVLV(QZER,STFS,VWNDR)
CCC---CALCULATE DIVERGENT WINDS (NO ROTATION)
      CALL EVLU(VLPS,QZER,UWNDD)
      CALL EVLV(VLPS,QZER,VWNDD)
CCC---transf SPECTRAL DIVERGENCE AND VORTICITY to grid
      CALL EVLSCA15(DIVS,DIV)
      CALL EVLSCA15(VORS,VOR)
      RETURN
      END
 
 
      SUBROUTINE EVLU(VELP,STRM,ARRP)
      PARAMETER(NW=15)
      PARAMETER(NLEG=NW+1,NMP=NW+1)
      PARAMETER(NLATG=40,NLONG=64,NLONGP=NLONG+1,IMAXT=3*NLONG/2)
      PARAMETER(EA=6.371E+06)
      COMMON/GAUSS/RADG(NLATG),GWGT(NLATG)
      COMMON/POLY1/PLEG(NLEG,NMP,NLATG),DPLEG(NLEG,NMP,NLATG)
      COMMON/FFTAR1/TRIGS(IMAXT),IFAX(10)
      COMMON/FFTAR2/ARRAY(NLONGP,NLATG),WORK(NLONGP,NLATG)
      DIMENSION VELP(2,NLEG,NMP),STRM(2,NLEG,NMP)
      DIMENSION ARRP(NLONG,NLATG)
      IM1=2*NMP+1
      IM2=NLONGP
      DO 200 J=1,NLATG
      CONST=EA*COS(RADG(J))
      DO 120 IM=1,NMP
      XM=IM-1
      YSUMR=0.0
      YSUMI=0.0
      IF(IM .GT. 1) THEN
         NLOW=1
      ELSE
         NLOW=2
      END IF
      DO 100 N=NLOW,NLEG
      XN=XM+N-1
      YSUMR=YSUMR+(-XM*VELP(2,N,IM)* PLEG(N,IM,J)
     1             -   STRM(1,N,IM)*DPLEG(N,IM,J))
      YSUMI=YSUMI+( XM*VELP(1,N,IM)* PLEG(N,IM,J)
     1             -   STRM(2,N,IM)*DPLEG(N,IM,J))
  100 CONTINUE
      ARRAY((IM-1)*2+1,J)=YSUMR/CONST
      ARRAY((IM-1)*2+2,J)=YSUMI/CONST
  120 CONTINUE
      DO 130 IM=IM1,IM2
      ARRAY(IM,J)=0.0
  130 CONTINUE
  200 CONTINUE
      INC=1
      ISIGN=+1
      JUMP=NLONGP
      LOT=NLATG
      CALL FFT991(ARRAY,WORK,TRIGS,IFAX,INC,JUMP,NLONG,LOT,ISIGN)
      DO 240 J=1,NLATG
      DO 240 I=1,NLONG
      ARRP(I,J)=ARRAY(I,J)
  240 CONTINUE
CC    CALL WNDWR(FIELDG)
      RETURN
      END
 
      SUBROUTINE EVLV(VELP,STRM,ARRP)
      PARAMETER(NW=15)
      PARAMETER(NLEG=NW+1,NMP=NW+1)
      PARAMETER(NLATG=40,NLONG=64,NLONGP=NLONG+1,IMAXT=3*NLONG/2)
      PARAMETER(EA=6.371E+06)
      COMMON/GAUSS/RADG(NLATG),GWGT(NLATG)
      COMMON/POLY1/PLEG(NLEG,NMP,NLATG),DPLEG(NLEG,NMP,NLATG)
      COMMON/FFTAR1/TRIGS(IMAXT),IFAX(10)
      COMMON/FFTAR2/ARRAY(NLONGP,NLATG),WORK(NLONGP,NLATG)
      DIMENSION VELP(2,NLEG,NMP),STRM(2,NLEG,NMP)
      DIMENSION ARRP(NLONG,NLATG)
      IM1=2*NMP+1
      IM2=NLONGP
      DO 200 J=1,NLATG
      CONST=EA*COS(RADG(J))
      DO 120 IM=1,NMP
      XM=IM-1
      YSUMR=0.0
      YSUMI=0.0
      IF(IM .GT. 1) THEN
         NLOW=1
      ELSE
         NLOW=2
      END IF
      DO 100 N=NLOW,NLEG
      XN=XM+N-1
      YSUMR=YSUMR+(-XM*STRM(2,N,IM)* PLEG(N,IM,J)
     1             +   VELP(1,N,IM)*DPLEG(N,IM,J))
      YSUMI=YSUMI+( XM*STRM(1,N,IM)* PLEG(N,IM,J)
     1             +   VELP(2,N,IM)*DPLEG(N,IM,J))
  100 CONTINUE
      ARRAY((IM-1)*2+1,J)=YSUMR/CONST
      ARRAY((IM-1)*2+2,J)=YSUMI/CONST
  120 CONTINUE
      DO 130 IM=IM1,IM2
      ARRAY(IM,J)=0.0
  130 CONTINUE
  200 CONTINUE
      INC=1
      ISIGN=+1
      JUMP=NLONGP
      LOT=NLATG
      CALL FFT991(ARRAY,WORK,TRIGS,IFAX,INC,JUMP,NLONG,LOT,ISIGN)
      DO 240 J=1,NLATG
      DO 240 I=1,NLONG
      ARRP(I,J)=ARRAY(I,J)
  240 CONTINUE
      RETURN
      END
 
 
      SUBROUTINE DEL2IN(ARRAYI,ARRAYO)
      PARAMETER(NW=15)
      PARAMETER(NLEG=NW+1,NM=NW,NMP=NM+1)
      PARAMETER(EA=6.371E+06)
      DIMENSION ARRAYI(2,NLEG,NMP),ARRAYO(2,NLEG,NMP)
      DO 100 IM=1,NMP
      XM=IM-1
      DO 80 N=1,NLEG
      XN=N+XM-1.0
      IF(XN .LT. .001) THEN
         ARRAYO(1,N,IM)=0.0
         ARRAYO(2,N,IM)=0.0
      ELSE
         DELSQI=-EA*EA/(XN*(XN+1.0))
         ARRAYO(1,N,IM)=ARRAYI(1,N,IM)*DELSQI
         ARRAYO(2,N,IM)=ARRAYI(2,N,IM)*DELSQI
      END IF
   80 CONTINUE
  100 CONTINUE
      RETURN
      END
 
      SUBROUTINE GCMCRC(UVEC,VVEC,DIV,VOR)
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C   PROGRAM TO TAKE THE (U,V) COMPONENTS OF THE WIND ON THE G. GRID    C
C   AND COMPUTE SPECTRAL DIVERGENCE (DIV) AND VORTICITY (VOR)          C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      PARAMETER(NW=15)
      PARAMETER(NLEG=NW+1,NM=NW,NMP=NM+1)
      PARAMETER(NLATG=40,NLONG=64,NLONGP=NLONG+1,IMAXT=3*NLONG/2)
      PARAMETER(EA=6.371E+06)
      COMMON/POLY1/PLEG(NLEG,NMP,NLATG),DPLEG(NLEG,NMP,NLATG)
      COMMON/GAUSS/RADG(NLATG),GWGT(NLATG)
      DIMENSION UVEC(NLONG,NLATG),VVEC(NLONG,NLATG)
      DIMENSION DIV(2,NLEG,NMP),VOR(2,NLEG,NMP)
      DIMENSION USPEC(2,NMP,NLATG),VSPEC(2,NMP,NLATG)
      DIMENSION UM(NLONGP),VM(NLONGP)
      CALL FTRANS15(UVEC,USPEC)
      CALL FTRANS15(VVEC,VSPEC)
      IM1=2*NMP+1
      IM2=NLONGP
      CALL FILLSP(DIV,0.0,NLEG,NMP)
      CALL FILLSP(VOR,0.0,NLEG,NMP)
      DO 300 J=1,NLATG
      COSR=COS(RADG(J))
      DO 120 IM=1,NMP
      DO 120 IC=1,2
      UM((IM-1)*2+IC)=USPEC(IC,IM,J)*COSR
      VM((IM-1)*2+IC)=VSPEC(IC,IM,J)*COSR
  120 CONTINUE
      DO 130 IM=IM1,IM2
      UM(IM)=0.0
      VM(IM)=0.0
  130 CONTINUE
      DO 200 IM=1,NMP
      XM=(IM-1)
      UREAL=UM((IM-1)*2+1)
      UIMAG=UM((IM-1)*2+2)
      VREAL=VM((IM-1)*2+1)
      VIMAG=VM((IM-1)*2+2)
      DO 180 N=1,NLEG
      DRE=(-XM*UIMAG*PLEG(N,IM,J)-VREAL*DPLEG(N,IM,J))/(COSR*COSR)
      DIM=(+XM*UREAL*PLEG(N,IM,J)-VIMAG*DPLEG(N,IM,J))/(COSR*COSR)
      DIV(1,N,IM)=DIV(1,N,IM)+DRE*GWGT(J)
      DIV(2,N,IM)=DIV(2,N,IM)+DIM*GWGT(J)
  180 CONTINUE
      DO 190 N=1,NLEG
      VRE=(-XM*VIMAG*PLEG(N,IM,J)+UREAL*DPLEG(N,IM,J))/(COSR*COSR)
      VIM=(+XM*VREAL*PLEG(N,IM,J)+UIMAG*DPLEG(N,IM,J))/(COSR*COSR)
      VOR(1,N,IM)=VOR(1,N,IM)+VRE*GWGT(J)
      VOR(2,N,IM)=VOR(2,N,IM)+VIM*GWGT(J)
  190 CONTINUE
  200 CONTINUE
  300 CONTINUE
      DO 310 IM=1,NMP
      DO 310 N=1,NLEG
      DO 310 IC=1,2
      DIV(IC,N,IM)=DIV(IC,N,IM)/EA
  310 CONTINUE
      DO 320 IM=1,NMP
      DO 320 N=1,NLEG
      DO 320 IC=1,2
      VOR(IC,N,IM)=VOR(IC,N,IM)/EA
  320 CONTINUE
      RETURN
      END
 
      SUBROUTINE FILLSP(ARRAYS,VALUE,NLEG,NMP)
      DIMENSION ARRAYS(2,NLEG,NMP)
      DO 20 IM=1,NMP
      DO 20 N=1,NLEG
      ARRAYS(1,N,IM)=VALUE
      ARRAYS(2,N,IM)=VALUE
   20 CONTINUE
      RETURN
      END
 
      SUBROUTINE GRDCMF(UVEC,VVEC,CMFGRD)
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C   PROGRAM TO TAKE COMPONENTS OF ANY FLUX (UVEC,VVEC) ON THE GRID,    C
C   AND COMPUTE CONVERGENCE WITH FINITE-DIFFERENCES                    C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      PARAMETER(NLATG=40,NLONG=64,NLATGM=NLATG-1)
      PARAMETER(EA=6.371E+06)
      COMMON/GAUSS/RADG(NLATG),GWGT(NLATG)
      DIMENSION UVEC(NLONG,NLATG),VVEC(NLONG,NLATG),CMFGRD(NLONG,NLATG)
      DIMENSION XLAT(NLATG)
      DIMENSION IP1(NLONG),IM1(NLONG)
      PI=ACOS(-1.0)
      XNLONG=NLONG
      DO 10 J=1,NLATG
      XLAT(J)=SIN(RADG(J))
   10 CONTINUE
      DLON=2.0*PI/XNLONG
      DO 30 I=1,NLONG
      IP1(I)=I+1
      IM1(I)=I-1
      IF(I .EQ. NLONG) IP1(I)=1
      IF(I .EQ.     1) IM1(I)=NLONG
   30 CONTINUE
      DO 120 J=2,NLATGM
      COSP=COS(RADG(J+1))
      COSR=COS(RADG(J  ))
      COSM=COS(RADG(J-1))
      DLAT=(XLAT(J+1) - XLAT(J-1))
      DO 100 I=1,NLONG
      XDIFF=(UVEC(IP1(I),J) - UVEC(IM1(I),J))/(COSR*DLON)
      YDIFF=(VVEC(I,J+1)*COSP - VVEC(I,J-1)*COSM)/DLAT
      CMFGRD(I,J)=-(XDIFF+YDIFF)/EA
  100 CONTINUE
  120 CONTINUE
      RETURN
      END
